mapscripts ReonekyRoom_MapScripts {
    MAP_SCRIPT_ON_FRAME_TABLE [
        VAR_REONEKY_ROOM_STATE, 0: ReonekyRoom_OnFrame
    ]
    MAP_SCRIPT_ON_TRANSITION: ReonekyRoom_OnTransition
}

script ReonekyRoom_OnTransition {
    if(var(VAR_REONEKY_ROOM_STATE) == 0) {
        setflag(FLAG_REONEKYROOM_HIDE_FIRE)
        setflag(FLAG_REONEKYROOM_HIDE_HAUNTER)
    }
    if(var(VAR_REONEKY_ROOM_STATE) == 1) {
        setobjectmovementtype2(LOCALID_REOROOM_REONEKY, MOVEMENT_TYPE_FACE_DOWN)
        setobjectmovementtype(LOCALID_REOROOM_REONEKY, MOVEMENT_TYPE_FACE_DOWN)
        setobjectxy(LOCALID_REOROOM_REONEKY, 6, 4)
        setobjectxyperm(LOCALID_REOROOM_REONEKY, 6, 4)
    }
    end
}

script ReonekyRoom_OnFrame {
    release
    applymovementandwait(LOCALID_PLAYER, Common_Movement_WalkUp2)
    special(SpawnCameraObject)
    directcameramovementtopos(5, 7)
    waitmovement(0)
    applymovementandwait(LOCALID_REOROOM_REONEKY, Common_Movement_WalkInPlaceRight)
    msgbox(format("¡Typhlosion, mantén la posición!"), MSGBOX_AUTOCLOSE)
    playse(SE_PIN)
    applymovementandwait(LOCALID_REOROOM_TYPHLOSION, Common_Movement_DoubleExclamationMark)
    release
    delay(80)
    msgbox(format("¡Esa grúa poseída encarna la injusticia administrativa!"))
    playse(SE_PIN)
    playmoncry(SPECIES_HAUNTER, CRY_MODE_ENCOUNTER)
    applymovementandwait(LOCALID_REOROOM_CRANE, moves(
        emote_xd
        jump_in_place_left * 2
        delay_16
        jump_in_place_left))
    playse(SE_PIN)
    applymovement(LOCALID_REOROOM_REONEKY, moves(emote_angry, jump_in_place_right * 2, delay_16 * 3))
    msgbox(format("¡No te rías, espectro de la ineficiencia!"), MSGBOX_AUTOCLOSE)
    waitmovement(0)
    delay(30)
    msgbox(format("Fueron solo unos minutos, ¡no pienso rendirme ante esta multa injusta!"))
    msgbox(format("¡Typhlosion, ataca!"))
    playse(SE_PIN)
    applymovementandwait(LOCALID_REOROOM_TYPHLOSION, Common_Movement_VersusWithDelay48)
    playse(SE_M_THUNDERBOLT2)
    applymovementandwait(LOCALID_REOROOM_TYPHLOSION, moves(
        lock_facing_direction
        walk_fast_right
        jump_in_place_right
        walk_fast_left
        unlock_facing_direction
    ))
    applymovementandwait(LOCALID_REOROOM_CRANE, moves(
        emote_x
        lock_facing_direction
        walk_in_place_left
        walk_right
        delay_16
        unlock_facing_direction))
    release
    delay(30)
    applymovementandwait(LOCALID_REOROOM_REONEKY, Common_Movement_WalkLeftLockFacingDirection)
    msgbox(format("¡Bien! ¡Ahora Rueda Fuego!"), MSGBOX_AUTOCLOSE)
    delay(20)
    applymovementandwait(LOCALID_REOROOM_TYPHLOSION, Common_Movement_JumpLeftLockFacingDirection)
    playse(SE_M_FLAME_WHEEL)
    clearflag(FLAG_REONEKYROOM_HIDE_FIRE)
    addobject(LOCALID_REOROOM_FIREATTACK)
    showobjectat(LOCALID_REOROOM_FIREATTACK, MAP_REONEKY_ROOM)
	setobjectsubpriority(LOCALID_REOROOM_FIREATTACK, MAP_REONEKY_ROOM, 0)
    applymovementandwait(LOCALID_REOROOM_TYPHLOSION, Common_Movement_JumpInPlaceRight2)
    playse(SE_M_FIRE_PUNCH)
    applymovementandwait(LOCALID_REOROOM_FIREATTACK, Common_Movement_WalkRight3)
    release
    delay(30)
    applymovementandwait(LOCALID_REOROOM_CRANE, Common_Movement_CraneStartBurning)
    setobjectmovementtype2(LOCALID_REOROOM_CRANE, MOVEMENT_TYPE_CRANE_BURNING_UP)
    playmoncry(SPECIES_HAUNTER, CRY_MODE_ENCOUNTER)
    fadescreen(FADE_TO_WHITE)
    setflag(FLAG_REONEKYROOM_HIDE_FIRE)
    removeobject(LOCALID_REOROOM_FIREATTACK)
    turnobject(LOCALID_REOROOM_CRANE, DIR_SOUTH)
    clearflag(FLAG_REONEKYROOM_HIDE_HAUNTER)
    addobject(LOCALID_REOROOM_HAUNTER)
    showobjectat(LOCALID_REOROOM_HAUNTER, MAP_REONEKY_ROOM)
    delay(30)
    release
    fadescreen(FADE_FROM_WHITE)
    playse(SE_PIN)
    applymovementandwait(LOCALID_REOROOM_HAUNTER, moves(
        emote_cry
        walk_fast_right
        walk_fast_down * 2
        walk_fast_left * 2
        delay_16
        set_invisible
        delay_8
        set_visible
        delay_8
        set_invisible
        delay_16
        set_visible
        delay_8
        set_invisible
        delay_16
        set_visible
        delay_8
        set_invisible
    ))
    setflag(FLAG_REONEKYROOM_HIDE_HAUNTER)
    removeobject(LOCALID_REOROOM_HAUNTER)
    applymovement(LOCALID_REOROOM_REONEKY, Common_Movement_JumpInPlaceRight2)
    msgbox(format("¡Sí!"), MSGBOX_AUTOCLOSE)
    waitmovement(0)
    delay(20)
    applymovementandwait(LOCALID_REOROOM_TYPHLOSION, Common_Movement_WalkInPlaceLeft)
    release
    msgbox(format("¡Lo hemos logrado! ¡Por fin nos libramos del fantasma de la multa!"), MSGBOX_AUTOCLOSE)
    playse(SE_PIN)
    applymovement(LOCALID_REOROOM_REONEKY, Common_Movement_HappyWithDelay48)
    applymovement(LOCALID_REOROOM_TYPHLOSION, moves(emote_happy, walk_in_place_fast_left * 3))
    waitmovement(LOCALID_REOROOM_REONEKY)
    release
    msgbox(format("Y Keltill decía que nada malo podía pasar con J15 al mando..."))
    playse(SE_PIN)
    applymovementandwait(LOCALID_REOROOM_REONEKY, Common_Movement_SweatDropWithDelay48)
    delay(20)
    release
    msgbox(format("Buen trabajo, regresa a tu Poké Ball."), MSGBOX_AUTOCLOSE)
    applymovementandwait(LOCALID_REOROOM_TYPHLOSION, Common_Movement_JumpInPlaceLeft2)
    fadescreen(FADE_TO_WHITE)
    setflag(FLAG_REONEKYROOM_HIDE_TYPHLOSION)
    removeobject(LOCALID_REOROOM_TYPHLOSION)
    fadescreen(FADE_FROM_WHITE)
    setvar(VAR_REONEKY_ROOM_STATE, 1)
    moveobjecttopos(LOCALID_REOROOM_REONEKY, 6, 4, DIR_SOUTH)
    waitmovement(0)
    directcameramovementtopos(6, 11)
    waitmovement(0)
    setobjectxyperm(LOCALID_REOROOM_REONEKY, 6, 4)
    setobjectmovementtype2(LOCALID_REOROOM_REONEKY, MOVEMENT_TYPE_FACE_DOWN)
    special(RemoveCameraObject)
    release
    end
}

script ReonekyRoom_EventScript_Reoneky {
    if(flag(FLAG_DEFEATED_ADMIN_REONEKY)) {
        lock
        release
        faceplayer
        playse(SE_PIN)
        applymovement(LOCALID_REOROOM_REONEKY, Common_Movement_QuestionMarkWithDelay48)
        msgbox(format("¿Otra vez por aquí?"), MSGBOX_AUTOCLOSE)
        waitmovement(0)
        delay(40)
        msgbox(format("Relájate, ya me ganaste… Sigue adelante que todavía tienes más enfrentamientos por delante.\pYo me quedaré contemplando cómo arde la grúa, a veces los problemas se consumen solos."), MSGBOX_AUTOCLOSE)
        release
        end
    }
    lock
    release
    faceplayer
    msgbox(format("Hola tí{OA}. Supongo que a esta altura ya sabrás qué hago aquí, así que me ahorraré explicaciones…\pSoy Reoneky, soy introspectivo y me gusta hacer las cosas a mi manera, sin que nadie me moleste."), MSGBOX_AUTOCLOSE)
    playse(SE_PIN)
    applymovementandwait(LOCALID_REOROOM_REONEKY, Common_Movement_ThinkingWithDelay48)
    msgbox(format("Hago lo que quiero, cuando quiero y como quiero.\pNi tú, ni nadie me va a impedir disfrutar esta celebración.\pPrepárate para acordarte de mí, ¡te voy a hacer morder el polvo!"))
    playse(SE_PIN)
    applymovementandwait(LOCALID_REOROOM_REONEKY, moves(
        emote_versus, 
        lock_facing_direction
        jump_in_place_down * 2
        unlock_facing_direction
        face_player))
    trainerbattle_no_intro(TRAINER_WAH_ADMIN_REONEKY_MAIN, format("Vaya… al final resultó más interesante de lo que esperaba."))
    msgbox(format("Así que este es el polvo que iba a hacerte morder, ¿eh?\pBueno, supongo que hoy me tocó a mí."), MSGBOX_AUTOCLOSE)
    playse(SE_PIN)
    applymovementandwait(LOCALID_REOROOM_REONEKY, Common_Movement_SweatDropWithDelay48)
    delay(20)
    msgbox(format("No suelo reconocerlo, pero… me sorprendiste. Tienes cabeza fría, eso me gusta."), MSGBOX_AUTOCLOSE)
    call(PokemonLeague_EliteFour_OpenRoomDoorMetatiles)
    delay(30)
    msgbox(format("Las puertas ya están abiertas. No te detengas ahora."), MSGBOX_AUTOCLOSE)
    setflag(FLAG_DEFEATED_ADMIN_REONEKY)
    release
    end
}




