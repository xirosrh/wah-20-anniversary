mapscripts Lobby_MapScripts {
    MAP_SCRIPT_ON_TRANSITION: Lobby_OnTransition
}

script Lobby_OnTransition {
    setrespawn(HEAL_LOCATION_LOBBY)
    end
}

script Lobby_EventScript_StartChallenge {
    lock
    applymovementandwait(LOCALID_LOBBY_COPE, Common_Movement_FacePlayer)
    playse(SE_PIN)
    applymovementandwait(LOCALID_LOBBY_COPE, Common_Movement_DoubleExclamationMarkWithDelay48)
    msgbox(format("¡Hola {PLAYER}!"), MSGBOX_AUTOCLOSE, speaker = SP_NAME_COPE)
    turnobject(LOCALID_PLAYER, DIR_WEST)
    delay(20)
    applymovementandwait(LOCALID_LOBBY_COPE, Common_Movement_WalkInPlaceRight)
    msgbox(format("¿Así que ya estás list{OA} para intentar el desafío?"), MSGBOX_YESNO, speaker = SP_NAME_COPE)
    waitmovement(0)
    closemessage
    delay(10)
    if(var(VAR_RESULT) == YES) {
        applymovementandwait(LOCALID_PLAYER, Common_Movement_WalkInPlaceLeft)
        msgbox(format("¡Excelente! Adelante, ¡te deseo mucha suerte y sobre todo que te diviertas!"), MSGBOX_AUTOCLOSE, speaker = SP_NAME_COPE)
        turnobject(LOCALID_PLAYER, DIR_NORTH)
        call(WahRoomsShared_OpenDoor)
        applymovementandwait(LOCALID_PLAYER, Common_Movement_WalkUp)
        hideobjectat(LOCALID_PLAYER, MAP_LOBBY)
        warp(MAP_OMEGA_ROOM, WARP_ID_OMEGA_ROOM_INITIAL)
        waitstate
    } else {
        playse(SE_PIN)
        applymovement(LOCALID_LOBBY_COPE, Common_Movement_SurpriseWithDelay48)
        msgbox(format("¿Así que no te sientes list{OA}?"), MSGBOX_AUTOCLOSE, speaker = SP_NAME_COPE)
        waitmovement(0)
        delay(20)
        msgbox(format("Recuerda que las personas del Lobby pueden ayudar en tu preparación."), MSGBOX_AUTOCLOSE, speaker = SP_NAME_COPE)
        applymovementandwait(LOCALID_PLAYER, Common_Movement_WalkDown)
        turnobject(LOCALID_LOBBY_COPE, DIR_SOUTH)
    }
    release
    end
}


script Lobby_EventScript_TeamSelector {
    lock
    faceplayer
    msgbox(format("¡Hola! Soy el encargado de la selección de equipos para el desafío.\pVeo que equipo actual es bueno, pero creo que puedes conseguir uno mejor.\p¿Quieres elegir otro equipo?"), MSGBOX_YESNO)
    if(var(VAR_RESULT) == YES) {
        special(OpenTeamSelectorFromField)
        waitstate
        if(var(VAR_RESULT) == TRUE) {
            msgbox( format("¡Excelente elección! Eso sí es un equipo ganador.\p¡Ve por la gloria!"), MSGBOX_AUTOCLOSE)
        } else {
            msgbox(Lobby_Text_TeamSelector_TeamSelectionCancelled, MSGBOX_AUTOCLOSE)
        }
    } else {
        msgbox(Lobby_Text_TeamSelector_TeamSelectionCancelled, MSGBOX_AUTOCLOSE)
    }
    turnobject(VAR_LAST_TALKED, DIR_SOUTH)
    release
    end
}


text Lobby_Text_TeamSelector_TeamSelectionCancelled {
    "¡Vaya, vaya! Un{A} chic{OA} valiente, ¿eh?\nAsí que piensas ganar con ESE equipo…\p¡Buena suerte, la vas a necesitar!"
}


script Lobby_EventScript_Shop {
	lock
    faceplayer
	message(format("¡Hola! ¿En qué puedo ayudarte?"))
	waitmessage
	pokemart(Lobby_MartItems)
	msgbox(format("Gracias por tu visita. ¡Que tengas un buen día!"), MSGBOX_AUTOCLOSE)
	release
    end
}

mart Lobby_MartItems {
	ITEM_HYPER_POTION
	ITEM_MAX_POTION
    ITEM_FULL_RESTORE
	ITEM_ETHER
    ITEM_MAX_ETHER
    ITEM_ELIXIR
    ITEM_MAX_ELIXIR
    ITEM_REVIVE
    ITEM_MAX_REVIVE
    ITEM_REVIVAL_HERB
    ITEM_BAXCALIBURITE
    ITEM_DRAGALGITE
    ITEM_TYRANITARITE
    ITEM_DARKRANITE
    ITEM_ALAKAZITE
    ITEM_LUCARIONITE
    ITEM_LUCARIONITE_Z
    ITEM_MEWTWONITE_X
    ITEM_MEWTWONITE_Y
    ITEM_CHARIZARDITE_X
    ITEM_CHARIZARDITE_Y
    ITEM_GALLADITE
    ITEM_GLALITITE
    ITEM_FROSLASSITE
    ITEM_CLEFABLITE
    ITEM_HOUNDOOMINITE
    ITEM_AGGRONITE
    ITEM_ZYGARDITE
    ITEM_VICTREEBELITE
}

script Lobby_EventScript_Roxas_IntroFirstBattle {
    checkplayergender
    if(var(VAR_RESULT) == MALE) {
        msgbox(format("¡Apa, mira vos!  ¡El nuevo campeón! ¡Felicidades loco!"), MSGBOX_AUTOCLOSE, speaker = SP_NAME_ROXAS)
    } else {
        msgbox(format("¡Apa, mira vos! ¡La nueva campeona! ¡Felicidades loca!"), MSGBOX_AUTOCLOSE, speaker = SP_NAME_ROXAS)
    }
    playse(SE_LEDGE)
    applymovementandwait(VAR_LAST_TALKED, moves(lock_facing_direction, jump_in_place_down))
    playse(SE_LEDGE)
    applymovementandwait(VAR_LAST_TALKED, moves(jump_in_place_down, unlock_facing_direction, face_player, delay_16 * 2))
    msgbox(format("Veo que se la diste a esos traidores de las salas… Me alegra que alguien les bajara los humos."), MSGBOX_AUTOCLOSE, speaker = SP_NAME_ROXAS)
    playse(SE_PIN)
    applymovementandwait(VAR_LAST_TALKED, Common_Movement_HappyWithDelay48)
    delay(20)
    msgbox(format("¿Te acordas de lo que te conté la ultima vez?"), MSGBOX_AUTOCLOSE, speaker = SP_NAME_ROXAS)
    playse(SE_PIN)
    applymovementandwait(VAR_LAST_TALKED, Common_Movement_QuestionMarkWithDelay48)
    msgbox(format("Terminé mi investigación en el Archipiélago Suzuka… es una historia larga, tendrías que haberlo visto…\pNo sabes lo que fue… Pero logré dar con el rastro de esa fuerza que estaba causando tantos males y estoy listo para probar si realmente este bicho es tan legendario como dicen las crónicas del archipiélago."), MSGBOX_AUTOCLOSE, speaker = SP_NAME_ROXAS)
    call(Lobby_EventScript_WalkInPlaceLastTalkedFacingPlayer)
    delay(20)
    msgbox(format("Vamos a hacerlo más interesante: un mano a mano.\pElige a tu mejor bicho y prepárate."), MSGBOX_AUTOCLOSE, speaker = SP_NAME_ROXAS)
    return
}

script Lobby_EventScript_Roxas_IntroRematch {
    msgbox(format("¡Apa, tu de nuevo! ¿Quieres volver a combatir contra mi legendario?"), MSGBOX_YESNO, speaker = SP_NAME_ROXAS)
    closemessage
    delay(20)
    if(var(VAR_RESULT) == YES) {
        msgbox(format("¡Vamos de nuevo! Elige a tu mejor bicho y prepárate."), speaker = SP_NAME_ROXAS)
        return
    } else {
        msgbox(Lobby_Text_Roxas_PlayerCancelled, MSGBOX_AUTOCLOSE, speaker = SP_NAME_ROXAS)
        releaseall
        end
    }
}

script Lobby_EventScript_Roxas {
    if(flag(FLAG_WAH_CHALLENGE_COMPLETED) == TRUE){
        lockall
        faceplayer
        playse(SE_PIN)
        applymovementandwait(VAR_LAST_TALKED, Common_Movement_DoubleExclamationMarkWithDelay48)
        if(flag(FLAG_DEFEATED_LOBBY_ROXAS)) {
            call(Lobby_EventScript_Roxas_IntroRematch)
        } else {
            call(Lobby_EventScript_Roxas_IntroFirstBattle)
        }
        special(SavePlayerParty)
        setvar(VAR_FRONTIER_FACILITY, FRONTIER_FACILITY_TOWER)
        setvar(VAR_0x8005, 1)
        setvar(VAR_0x8004, FRONTIER_LVL_OPEN)
        fadescreen(FADE_TO_BLACK)
        special(ChoosePartyForBattleFrontier)
        waitstate
        lockall
        if (var(VAR_RESULT) == 0) {
            special(LoadPlayerParty)
            msgbox(Lobby_Text_Roxas_PlayerCancelled, MSGBOX_AUTOCLOSE, speaker = SP_NAME_ROXAS)
            releaseall
            end
        }
        special(ReducePlayerPartyToSelectedMons)
        msgbox(format("¡A ver si te la bancas!"), MSGBOX_AUTOCLOSE, speaker = SP_NAME_ROXAS)
        setflag(B_FLAG_NO_WHITEOUT)
        trainerbattle_no_intro(TRAINER_WAH_ADMIN_ROXAS, format("¡Nooooo!"))
        clearflag(B_FLAG_NO_WHITEOUT)
        special(GetBattleOutcome)
        special(LoadPlayerParty)
        special(HealPlayerParty)
        if (var(VAR_RESULT) == B_OUTCOME_WON) {
            playse(SE_PIN)
            applymovement(VAR_LAST_TALKED, Common_Movement_X)
            msgbox(format("¡Nooo, me estás cargando! Ni con la fuerza de este Pokémon oscuro te pude ganar.\pAhora me cierra por qué a vos te dejaron pasar a las salas y a mí me sacaron de un voleo.\pTenés algo distinto, pasta de campeón, no es solo fuerza bruta.\pLa verdad, me cerraste la boca. Bien ganado."), MSGBOX_DEFAULT)
            waitmovement(0)
            setflag(FLAG_DEFEATED_LOBBY_ROXAS)
        } else {
            checkplayergender
            if(var(VAR_RESULT) == MALE) {
                msgbox(format("Mamita lo que me imaginaba… El poder de este bicho es de otro planeta, ni vos que sos el campeón te la bancaste."), MSGBOX_AUTOCLOSE, speaker = SP_NAME_ROXAS)
            } else {
                msgbox(format("Mamita lo que me imaginaba… El poder de este bicho es de otro planeta, ni vos que sos la campeona te la bancaste."), MSGBOX_AUTOCLOSE, speaker = SP_NAME_ROXAS)
            }
            playse(SE_PIN)
            applymovementandwait(VAR_LAST_TALKED, Common_Movement_SweatDropWithDelay48)
            msgbox(format("Si algún día te das una vuelta por el Archipiélago Suzuka, anda con cuidado maquina, porque allá este no te perdona ni media.\pSuerte la próxima."), MSGBOX_AUTOCLOSE, speaker = SP_NAME_ROXAS)
        }
        releaseall
        end
    } else {
        lockall
        faceplayer
        msgbox(format("No te molestes en preguntar… A mí tampoco me dejan pasar estos…"), MSGBOX_AUTOCLOSE, speaker = SP_NAME_ROXAS)
        playse(SE_PIN)
        applymovementandwait(VAR_LAST_TALKED, Common_Movement_AnnoyedWithDelay48)
        delay(20)
        msgbox(format("Dicen que haber sido Líder de WAH en el Archipiélago Suzuka durante solo dos semanas no es “trayectoria suficiente” para participar."), MSGBOX_AUTOCLOSE, speaker = SP_NAME_ROXAS)
        delay(20)
        playse(SE_PIN)
        applymovementandwait(VAR_LAST_TALKED, Common_Movement_Angry)
        playse(SE_LEDGE)
        applymovementandwait(VAR_LAST_TALKED, moves(lock_facing_direction, jump_in_place_down))
        playse(SE_LEDGE)
        applymovement(VAR_LAST_TALKED, moves(jump_in_place_down, unlock_facing_direction, face_player, delay_16 * 2))
        msgbox(format("¡Unos caraduras loco!"), MSGBOX_AUTOCLOSE, speaker = SP_NAME_ROXAS)
        waitmovement(0)
        lockall
        delay(20)
        msgbox(format("Pero bueno, tampoco me voy a quedar acá llorando.\pEstoy tras un barco que desapareció al oeste de las Islas Naranja, el archipiélago Suzuka.\pEl Team Rocket anda haciendo de las suyas y dicen que hay un bicho oscuro con ojos rojos que es un peligro… Aterrador…\pHabrá que investigar y ver que onda…"), MSGBOX_AUTOCLOSE, speaker = SP_NAME_ROXAS)
        call(Lobby_EventScript_WalkInPlaceLastTalkedFacingPlayer)
        msgbox(format("Che, discúlpame, mejor me voy a seguir con mis investigaciones.\p¡Suerte, espero que a vos sí te den bola!"), MSGBOX_AUTOCLOSE, speaker = SP_NAME_ROXAS)
        releaseall
        end
    }
}

text Lobby_Text_Roxas_PlayerCancelled {
    format("¿Así que le tenés miedo a mi bicho legendario? ¡Y aún así ganaste las salas!")
}

script Lobby_EventScript_WalkInPlaceLastTalkedFacingPlayer {
    specialvar(VAR_RESULT, GetPlayerFacingDirection)
    switch(var(VAR_RESULT)) {
        case DIR_NORTH:
            applymovementandwait(VAR_LAST_TALKED, Common_Movement_WalkInPlaceDown)
            break
        case DIR_SOUTH:
            applymovementandwait(VAR_LAST_TALKED, Common_Movement_WalkInPlaceUp)
            break
        case DIR_WEST:
            applymovementandwait(VAR_LAST_TALKED,Common_Movement_WalkInPlaceRight)
            break
        case DIR_EAST:
            applymovementandwait(VAR_LAST_TALKED, Common_Movement_WalkInPlaceLeft)
            break
    }
    return
}


script Lobby_EventScript_FakemonDexMenu {
    lock
    faceplayer
    msgbox(format("Soy el experto en las especies exclusivas de Whack a Hack.\pTe revelaré todo sobre ellas, pero solo de las que hayas visto.\p¡No quiero arruinar el factor sorpresa!"), MSGBOX_AUTOCLOSE)
    playse(SE_PIN)
    applymovementandwait(VAR_LAST_TALKED, Common_Movement_SweatDropWithDelay48)
    goto(Lobby_EventScript_FakemonDexMenu_ChooseSpecies)
}

script Lobby_EventScript_FakemonDexMenu_ChooseSpecies {
    setvar(VAR_0x8000, 0)
    getseenmon(SPECIES_KAOSUNE)
    if(var(VAR_RESULT) != 0) {
        bufferspeciesname(STR_VAR_1, SPECIES_KAOSUNE)
        dynmultipush(gStringVar1, 0)
        addvar(VAR_0x8000, 1)
    }
    getseenmon(SPECIES_FREECH)
    if(var(VAR_RESULT) != 0) {
        bufferspeciesname(STR_VAR_1, SPECIES_FREECH)
        dynmultipush(gStringVar1, 1)
        addvar(VAR_0x8000, 1)
    }
    getseenmon(SPECIES_PLUGOINK)
    if(var(VAR_RESULT) != 0) {
        bufferspeciesname(STR_VAR_1, SPECIES_PLUGOINK)
        dynmultipush(gStringVar1, 2)
        addvar(VAR_0x8000, 1)
    }
    getseenmon(SPECIES_GRITESPEC)
    if(var(VAR_RESULT) != 0) {
        bufferspeciesname(STR_VAR_1, SPECIES_GRITESPEC)
        dynmultipush(gStringVar1, 3)
        addvar(VAR_0x8000, 1)
    }
    getseenmon(SPECIES_MAGIKEN)
    if(var(VAR_RESULT) != 0) {
        bufferspeciesname(STR_VAR_1, SPECIES_MAGIKEN)
        dynmultipush(gStringVar1, 4)
        addvar(VAR_0x8000, 1)
    }
    getseenmon(SPECIES_AYEWIRAZ)
    if(var(VAR_RESULT) != 0) {
        bufferspeciesname(STR_VAR_1, SPECIES_AYEWIRAZ)
        dynmultipush(gStringVar1, 5)
        addvar(VAR_0x8000, 1)
    }
    getseenmon(SPECIES_ELECTRODES)
    if(var(VAR_RESULT) != 0) {
        bufferspeciesname(STR_VAR_1, SPECIES_ELECTRODES)
        dynmultipush(gStringVar1, 6)
        addvar(VAR_0x8000, 1)
    }
    getseenmon(SPECIES_EING)
    if(var(VAR_RESULT) != 0) {
        bufferspeciesname(STR_VAR_1, SPECIES_EING)
        dynmultipush(gStringVar1, 7)
        addvar(VAR_0x8000, 1)
    }
    if(var(VAR_0x8000) == 0) {
        msgbox(format("¡Todas las especies que has visto son de público conocimiento!\p¡Ve a combatir a las salas y ya verás de las que estoy hablando!"))
        release
        end
    }
    dynmultipush("Salir", 8)
    msgbox(format("¿De cuál especie te gustaría conocer más?"))
    dynmultistack(0, 0, FALSE, 6, 0, 0, DYN_MULTICHOICE_CB_NONE)
    if(var(VAR_RESULT) > 7) {
        msgbox(format("¡Si quieres saber algo más, aquí estaré!"), MSGBOX_AUTOCLOSE)
        release
        end
    }
    switch(var(VAR_RESULT)) {
        case 0:
            showmonpic(SPECIES_KAOSUNE, 10, 3)
            setvar(VAR_0x8004, SPECIES_KAOSUNE)
            call(Lobby_EventScript_ShowFakemonDexInfo)
        case 1:
            showmonpic(SPECIES_FREECH, 10, 3)
            setvar(VAR_0x8004, SPECIES_FREECH)
            call(Lobby_EventScript_ShowFakemonDexInfo)
        case 2:
            showmonpic(SPECIES_PLUGOINK, 10, 3)
            setvar(VAR_0x8004, SPECIES_PLUGOINK)
            call(Lobby_EventScript_ShowFakemonDexInfo)
        case 3:
            showmonpic(SPECIES_GRITESPEC, 10, 3)
            setvar(VAR_0x8004, SPECIES_GRITESPEC)
            call(Lobby_EventScript_ShowFakemonDexInfo)
        case 4:
            showmonpic(SPECIES_MAGIKEN, 10, 3)
            setvar(VAR_0x8004, SPECIES_MAGIKEN)
            call(Lobby_EventScript_ShowFakemonDexInfo)
        case 5:
            showmonpic(SPECIES_AYEWIRAZ, 10, 3)
            setvar(VAR_0x8004, SPECIES_AYEWIRAZ)
            call(Lobby_EventScript_ShowFakemonDexInfo)
        case 6:
            showmonpic(SPECIES_ELECTRODES, 10, 3)
            setvar(VAR_0x8004, SPECIES_ELECTRODES)
            call(Lobby_EventScript_ShowFakemonDexInfo)
        case 7:
            showmonpic(SPECIES_EING, 10, 3)
            setvar(VAR_0x8004, SPECIES_EING)
            call(Lobby_EventScript_ShowFakemonDexInfo)
    }
    goto(Lobby_EventScript_FakemonDexMenu_ChooseSpecies)
}

script Lobby_EventScript_ShowFakemonDexInfo {
    special(BufferSpeciesDexDescriptionForTextbox)
    msgbox(format("Ya que no tienes una Pokedex, su descripción es:{FONT_SMALL}\p“{STR_VAR_1}”"))
    special(BufferSpeciesTypeNames)
    if(var(VAR_RESULT) == 1) {
        msgbox("Su tipo es: {STR_VAR_2}.")
    } else {
        msgbox("Sus tipos son: {STR_VAR_2} y {STR_VAR_3}.")
    }
    msgbox("¡Y esto seguramente no lo sabías!\nSus habilidades son:")
    setvar(VAR_0x8005, 0)
    special(BufferSpeciesAbilityNameAndDescription)
    if(var(VAR_RESULT) != 0) {
        msgbox(format("{STR_VAR_1}:\n{FONT_SMALL}“{STR_VAR_2}”"))
    }
    setvar(VAR_0x8005, 1)
    special(BufferSpeciesAbilityNameAndDescription)
    if(var(VAR_RESULT) != 0) {
        msgbox(format("{STR_VAR_1}:\n{FONT_SMALL}“{STR_VAR_2}”"))
    }
    setvar(VAR_0x8005, 2)
    special(BufferSpeciesAbilityNameAndDescription)
    if(var(VAR_RESULT) != 0) {
        msgbox(format("{STR_VAR_1}:\n{FONT_SMALL}“{STR_VAR_2}”"))
    }
    special(BufferSpeciesStats)
    msgbox(format("¡Último dato! ¿Qué tan buenos son sus stats base?\p{FONT_SMALL}{STR_VAR_1}"))
    hidemonpic()
    bufferspeciesname(STR_VAR_1, VAR_0x8004)
    msgbox("Y eso es todo sobre {STR_VAR_1}.\nEspero que te haya resultado útil.")
    return
}

