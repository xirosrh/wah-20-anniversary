mapscripts Lobby_MapScripts {
    MAP_SCRIPT_ON_TRANSITION: Lobby_OnTransition
}

script Lobby_OnTransition {
    setrespawn(HEAL_LOCATION_LOBBY)
    end
}

script Lobby_EventScript_StartChallenge {
    lock
    applymovementandwait(LOCALID_LOBBY_POLICE, Common_Movement_FacePlayer)
    msgbox(format("Pendiente textos, ¿empezar desafío?"), MSGBOX_YESNO)
    if(var(VAR_RESULT) == YES) {
        warp(MAP_OMEGA_ROOM, WARP_ID_OMEGA_ROOM_INITIAL)
        waitstate
    } else {
        applymovementandwait(LOCALID_PLAYER, Common_Movement_WalkDown)
        turnobject(LOCALID_LOBBY_POLICE, DIR_SOUTH)
    }
    release
    end
}


script Lobby_EventScript_TeamSelector {
    lock
    faceplayer
    msgbox(format("¡Hola! Soy el encargado de la selección de equipos para el desafío.\pVeo que equipo actual es bueno, pero creo que puedes conseguir uno mejor.\p¿Quieres elegir otro equipo?"), MSGBOX_YESNO)
    if(var(VAR_RESULT) == YES) {
        special(OpenTeamSelectorFromField)
        waitstate
        if(var(VAR_RESULT) == TRUE) {
            msgbox( format("¡Excelente elección! Eso sí es un equipo ganador.\p¡Ve por la gloria!"), MSGBOX_AUTOCLOSE)
        } else {
            msgbox(Lobby_Text_TeamSelector_TeamSelectionCancelled, MSGBOX_AUTOCLOSE)
        }
    } else {
        msgbox(Lobby_Text_TeamSelector_TeamSelectionCancelled, MSGBOX_AUTOCLOSE)
    }
    turnobject(VAR_LAST_TALKED, DIR_SOUTH)
    release
    end
}


text Lobby_Text_TeamSelector_TeamSelectionCancelled {
    "¡Vaya, vaya! Un{A} chic{OA} valiente, ¿eh?\nAsí que piensas ganar con ESE equipo…\p¡Buena suerte, la vas a necesitar!"
}


script Lobby_EventScript_Shop {
	lock
    faceplayer
	message(format("¡Hola! ¿En qué puedo ayudarte?"))
	waitmessage
	pokemart(Lobby_MartItems)
	msgbox(format("Gracias por tu visita. ¡Que tengas un buen día!"), MSGBOX_AUTOCLOSE)
	release
    end
}

mart Lobby_MartItems {
	ITEM_HYPER_POTION
	ITEM_MAX_POTION
    ITEM_FULL_RESTORE
	ITEM_ETHER
    ITEM_MAX_ETHER
    ITEM_ELIXIR
    ITEM_MAX_ELIXIR
    ITEM_REVIVE
    ITEM_MAX_REVIVE
    ITEM_REVIVAL_HERB
    ITEM_BAXCALIBURITE
    ITEM_DRAGALGITE
    ITEM_TYRANITARITE
    ITEM_DARKRANITE
    ITEM_ALAKAZITE
    ITEM_LUCARIONITE
    ITEM_LUCARIONITE_Z
    ITEM_MEWTWONITE_X
    ITEM_MEWTWONITE_Y
    ITEM_CHARIZARDITE_X
    ITEM_CHARIZARDITE_Y
    ITEM_GALLADITE
    ITEM_GLALITITE
    ITEM_FROSLASSITE
    ITEM_CLEFABLITE
    ITEM_HOUNDOOMINITE
    ITEM_AGGRONITE
    ITEM_ZYGARDITE
    ITEM_VICTREEBELITE
}

script Lobby_EventScript_Roxas_IntroFirstBattle {
    checkplayergender
    if(var(VAR_RESULT) == MALE) {
        msgbox(format("¡Apa, mira vos!  ¡El nuevo campeón! ¡Felicidades loco!"), MSGBOX_AUTOCLOSE, speaker = SP_NAME_ROXAS)
    } else {
        msgbox(format("¡Apa, mira vos! ¡La nueva campeona! ¡Felicidades loca!"), MSGBOX_AUTOCLOSE, speaker = SP_NAME_ROXAS)
    }
    playse(SE_LEDGE)
    applymovementandwait(VAR_LAST_TALKED, moves(lock_facing_direction, jump_in_place_down))
    playse(SE_LEDGE)
    applymovementandwait(VAR_LAST_TALKED, moves(jump_in_place_down, unlock_facing_direction, face_player, delay_16 * 2))
    msgbox(format("Veo que se la diste a esos traidores de las salas… Me alegra que alguien les bajara los humos."), MSGBOX_AUTOCLOSE, speaker = SP_NAME_ROXAS)
    playse(SE_PIN)
    applymovementandwait(VAR_LAST_TALKED, Common_Movement_HappyWithDelay48)
    delay(20)
    msgbox(format("¿Te acordas de lo que te conté la ultima vez?"), MSGBOX_AUTOCLOSE, speaker = SP_NAME_ROXAS)
    playse(SE_PIN)
    applymovementandwait(VAR_LAST_TALKED, Common_Movement_QuestionMarkWithDelay48)
    msgbox(format("Terminé mi investigación en el Archipiélago Suzuka… es una historia larga, tendrías que haberlo visto…\pNo sabes lo que fue… Pero logré dar con el rastro de esa fuerza que estaba causando tantos males y estoy listo para probar si realmente este bicho es tan legendario como dicen las crónicas del archipiélago."), MSGBOX_AUTOCLOSE, speaker = SP_NAME_ROXAS)
    call(Lobby_EventScript_WalkInPlaceLastTalkedFacingPlayer)
    delay(20)
    msgbox(format("Vamos a hacerlo más interesante: un mano a mano.\pElige a tu mejor bicho y prepárate."), MSGBOX_AUTOCLOSE, speaker = SP_NAME_ROXAS)
    return
}

script Lobby_EventScript_Roxas_IntroRematch {
    msgbox(format("¡Apa, tu de nuevo! ¿Quieres volver a combatir contra mi legendario?"), MSGBOX_YESNO, speaker = SP_NAME_ROXAS)
    closemessage
    delay(20)
    if(var(VAR_RESULT) == YES) {
        msgbox(format("¡Vamos de nuevo! Elige a tu mejor bicho y prepárate."), speaker = SP_NAME_ROXAS)
        return
    } else {
        msgbox(Lobby_Text_Roxas_PlayerCancelled, MSGBOX_AUTOCLOSE, speaker = SP_NAME_ROXAS)
        releaseall
        end
    }
}

script Lobby_EventScript_Roxas {
    if(flag(FLAG_WAH_CHALLENGE_COMPLETED) == TRUE){
        lockall
        faceplayer
        playse(SE_PIN)
        applymovementandwait(VAR_LAST_TALKED, Common_Movement_DoubleExclamationMarkWithDelay48)
        if(flag(FLAG_DEFEATED_LOBBY_ROXAS)) {
            call(Lobby_EventScript_Roxas_IntroRematch)
        } else {
            call(Lobby_EventScript_Roxas_IntroFirstBattle)
        }
        special(SavePlayerParty)
        setvar(VAR_FRONTIER_FACILITY, FRONTIER_FACILITY_TOWER)
        setvar(VAR_0x8005, 1)
        setvar(VAR_0x8004, FRONTIER_LVL_OPEN)
        fadescreen(FADE_TO_BLACK)
        special(ChoosePartyForBattleFrontier)
        waitstate
        lockall
        if (var(VAR_RESULT) == 0) {
            special(LoadPlayerParty)
            msgbox(Lobby_Text_Roxas_PlayerCancelled, MSGBOX_AUTOCLOSE, speaker = SP_NAME_ROXAS)
            releaseall
            end
        }
        special(ReducePlayerPartyToSelectedMons)
        msgbox(format("¡A ver si te la bancas!"), MSGBOX_AUTOCLOSE, speaker = SP_NAME_ROXAS)
        setflag(B_FLAG_NO_WHITEOUT)
        trainerbattle_no_intro(TRAINER_WAH_ADMIN_ROXAS, format("¡Nooooo!"))
        clearflag(B_FLAG_NO_WHITEOUT)
        special(GetBattleOutcome)
        special(LoadPlayerParty)
        special(HealPlayerParty)
        if (var(VAR_RESULT) == B_OUTCOME_WON) {
            playse(SE_PIN)
            applymovement(VAR_LAST_TALKED, Common_Movement_X)
            msgbox(format("¡Nooo, me estás cargando! Ni con la fuerza de este Pokémon oscuro te pude ganar.\pAhora me cierra por qué a vos te dejaron pasar a las salas y a mí me sacaron de un voleo.\pTenés algo distinto, pasta de campeón, no es solo fuerza bruta.\pLa verdad, me cerraste la boca. Bien ganado."), MSGBOX_DEFAULT)
            waitmovement(0)
            setflag(FLAG_DEFEATED_LOBBY_ROXAS)
        } else {
            checkplayergender
            if(var(VAR_RESULT) == MALE) {
                msgbox(format("Mamita lo que me imaginaba… El poder de este bicho es de otro planeta, ni vos que sos el campeón te la bancaste."), MSGBOX_AUTOCLOSE, speaker = SP_NAME_ROXAS)
            } else {
                msgbox(format("Mamita lo que me imaginaba… El poder de este bicho es de otro planeta, ni vos que sos la campeona te la bancaste."), MSGBOX_AUTOCLOSE, speaker = SP_NAME_ROXAS)
            }
            playse(SE_PIN)
            applymovementandwait(VAR_LAST_TALKED, Common_Movement_SweatDropWithDelay48)
            msgbox(format("Si algún día te das una vuelta por el Archipiélago Suzuka, anda con cuidado maquina, porque allá este no te perdona ni media.\pSuerte la próxima."), MSGBOX_AUTOCLOSE, speaker = SP_NAME_ROXAS)
        }
        releaseall
        end
    } else {
        lockall
        faceplayer
        msgbox(format("No te molestes en preguntar… A mí tampoco me dejan pasar estos…"), MSGBOX_AUTOCLOSE, speaker = SP_NAME_ROXAS)
        playse(SE_PIN)
        applymovementandwait(VAR_LAST_TALKED, Common_Movement_AnnoyedWithDelay48)
        delay(20)
        msgbox(format("Dicen que haber sido Líder de WAH en el Archipiélago Suzuka durante solo dos semanas no es “trayectoria suficiente” para participar."), MSGBOX_AUTOCLOSE, speaker = SP_NAME_ROXAS)
        delay(20)
        playse(SE_PIN)
        applymovementandwait(VAR_LAST_TALKED, Common_Movement_Angry)
        playse(SE_LEDGE)
        applymovementandwait(VAR_LAST_TALKED, moves(lock_facing_direction, jump_in_place_down))
        playse(SE_LEDGE)
        applymovement(VAR_LAST_TALKED, moves(jump_in_place_down, unlock_facing_direction, face_player, delay_16 * 2))
        msgbox(format("¡Unos caraduras loco!"), MSGBOX_AUTOCLOSE, speaker = SP_NAME_ROXAS)
        waitmovement(0)
        lockall
        delay(20)
        msgbox(format("Pero bueno, tampoco me voy a quedar acá llorando.\pEstoy tras un barco que desapareció al oeste de las Islas Naranja, el archipiélago Suzuka.\pEl Team Rocket anda haciendo de las suyas y dicen que hay un bicho oscuro con ojos rojos que es un peligro… Aterrador…\pHabrá que investigar y ver que onda…"), MSGBOX_AUTOCLOSE, speaker = SP_NAME_ROXAS)
        call(Lobby_EventScript_WalkInPlaceLastTalkedFacingPlayer)
        msgbox(format("Che, discúlpame, mejor me voy a seguir con mis investigaciones.\p¡Suerte, espero que a vos sí te den bola!"), MSGBOX_AUTOCLOSE, speaker = SP_NAME_ROXAS)
        releaseall
        end
    }
}

text Lobby_Text_Roxas_PlayerCancelled {
    format("¿Así que le tenés miedo a mi bicho legendario? ¡Y aún así ganaste las salas!")
}

script Lobby_EventScript_WalkInPlaceLastTalkedFacingPlayer {
    specialvar(VAR_RESULT, GetPlayerFacingDirection)
    switch(var(VAR_RESULT)) {
        case DIR_NORTH:
            applymovementandwait(VAR_LAST_TALKED, Common_Movement_WalkInPlaceDown)
            break
        case DIR_SOUTH:
            applymovementandwait(VAR_LAST_TALKED, Common_Movement_WalkInPlaceUp)
            break
        case DIR_WEST:
            applymovementandwait(VAR_LAST_TALKED,Common_Movement_WalkInPlaceRight)
            break
        case DIR_EAST:
            applymovementandwait(VAR_LAST_TALKED, Common_Movement_WalkInPlaceLeft)
            break
    }
    return
}

script Lobby_EventScript_Kaktus_IntroFirstBattle {
    msgbox("Vaya, vaya…\n¿Así que completaste el desafío?", MSGBOX_AUTOCLOSE, speaker = SP_NAME_INMORTAL_KAKTUS)
    playse(SE_PIN)
    applymovementandwait(VAR_LAST_TALKED, Common_Movement_SurpriseWithDelay48)
    msgbox(format("Entonces ya no eres un{A} novat{OA}. Veamos si sabes aplicar lo aprendido.\pUn combate, sin atajos, sin trucos. ¿List{OA}?"), MSGBOX_AUTOCLOSE, speaker = SP_NAME_INMORTAL_KAKTUS)
    specialvar(VAR_RESULT, GetPlayerFacingDirection)
    switch(var(VAR_RESULT)) {
        case DIR_NORTH:
            applymovementandwait(LOCALID_PLAYER, Common_Movement_WalkInPlaceUp)
        case DIR_SOUTH:
            applymovementandwait(LOCALID_PLAYER, Common_Movement_WalkInPlaceDown)
        case DIR_WEST:
            applymovementandwait(LOCALID_PLAYER, Common_Movement_WalkInPlaceLeft)
        case DIR_EAST:
            applymovementandwait(LOCALID_PLAYER, Common_Movement_WalkInPlaceRight)
    }
    return
}

script Lobby_EventScript_Kaktus_IntroRematch {
    msgbox(format("Ey, ¿qué tal? Mi Ludicolo sigue con ganas de bailar.\p¿Otro asalto?"), MSGBOX_YESNO, speaker = SP_NAME_INMORTAL_KAKTUS)
    closemessage
    delay(20)
    if(var(VAR_RESULT) == YES) {
        msgbox(format("¡Vamos!"), speaker = SP_NAME_INMORTAL_KAKTUS)
        return
    } else {
        msgbox(format("¡Será en otra oportunidad entonces!"), MSGBOX_AUTOCLOSE, speaker = SP_NAME_INMORTAL_KAKTUS)
        goto(Lobby_EventScript_Kaktus_OnFinishEvent)
    }

}

script Lobby_EventScript_Kaktus_OnFinishEvent {
    turnobject(VAR_LAST_TALKED, DIR_SOUTH)
    release
    end
}

script Lobby_EventScript_Kaktus {
    lock
    faceplayer
    if(flag(FLAG_WAH_CHALLENGE_COMPLETED) == TRUE){
         if(flag(FLAG_DEFEATED_LOBBY_KAKTUS)) {
            call(Lobby_EventScript_Kaktus_IntroRematch)
        } else {
            call(Lobby_EventScript_Kaktus_IntroFirstBattle)
        }
        
        playse(SE_PIN)
        applymovement(LOCALID_PLAYER, Common_Movement_VersusWithDelay48)
        applymovement(VAR_LAST_TALKED, Common_Movement_VersusWithDelay48)
        waitmovement(0)
        setflag(B_FLAG_NO_WHITEOUT)
        trainerbattle_no_intro(TRAINER_WAH_COLLABORATOR_KAKTUS, format("¿Acaso perdí?"))
        clearflag(B_FLAG_NO_WHITEOUT)
        special(GetBattleOutcome)
        special(HealPlayerParty)
        if (var(VAR_RESULT) == B_OUTCOME_WON) {
            playse(SE_PIN)
            applymovementandwait(VAR_LAST_TALKED, Common_Movement_XWithDelay48)
            msgbox(format("Ja… perdí."), MSGBOX_AUTOCLOSE, speaker = SP_NAME_INMORTAL_KAKTUS)
            playse(SE_PIN)
            applymovementandwait(VAR_LAST_TALKED, Common_Movement_ThinkingWithDelay48)
            call(Lobby_EventScript_WalkInPlaceLastTalkedFacingPlayer)
            msgbox(format("Pero esto es lo bonito de esta comunidad.\pSiempre hay alguien nuevo que te sorprende."), MSGBOX_AUTOCLOSE, speaker = SP_NAME_INMORTAL_KAKTUS)
            playse(SE_PIN)
            applymovementandwait(VAR_LAST_TALKED, Common_Movement_HeartWithDelay48)
            call(Lobby_EventScript_WalkInPlaceLastTalkedFacingPlayer)
            msgbox(format("Enhorabuena, crack."), MSGBOX_AUTOCLOSE, speaker = SP_NAME_INMORTAL_KAKTUS)
            setflag(FLAG_DEFEATED_LOBBY_KAKTUS)
        } else {
            msgbox(format("Eres buen{OA}, pero aún te falta entrenar para poder vencerme."), MSGBOX_AUTOCLOSE, speaker = SP_NAME_INMORTAL_KAKTUS)
            playse(SE_LEDGE)
            applymovementandwait(VAR_LAST_TALKED, moves(lock_facing_direction, jump_in_place_down))
            playse(SE_LEDGE)
            applymovement(VAR_LAST_TALKED, moves(jump_in_place_down, unlock_facing_direction, face_player))
            msgbox(format("¡Espero la revancha pronto!"), MSGBOX_AUTOCLOSE, speaker = SP_NAME_INMORTAL_KAKTUS)
            waitmovement(0)
        }
        goto(Lobby_EventScript_Kaktus_OnFinishEvent)
        
    } else {
        msgbox(format("¿Sabes? Mucha gente en la comunidad empezó viendo tutoriales míos."), MSGBOX_AUTOCLOSE, speaker = SP_NAME_INMORTAL_KAKTUS)
        delay(20)
        call(Lobby_EventScript_WalkInPlaceLastTalkedFacingPlayer)
        msgbox(format("Aunque si te soy sincero… el primero que hice me dio tanta verguenza que lo borré."), MSGBOX_AUTOCLOSE, speaker = SP_NAME_INMORTAL_KAKTUS)
        playse(SE_PIN)
        applymovementandwait(VAR_LAST_TALKED, Common_Movement_SweatDropWithDelay48)
        goto(Lobby_EventScript_Kaktus_OnFinishEvent)

    }
}

script Lobby_EventScript_Gameboycl_IntroFirstBattle {
    delay(20)
    playse(SE_PIN)
    applymovement(VAR_LAST_TALKED, Common_Movement_ThinkingWithDelay48)
    msgbox(format("No soy competitivo ni ambicioso. Soy más bien una persona mediocre."), MSGBOX_AUTOCLOSE, speaker = SP_NAME_GAMEBOY_CL)
    waitmovement(0)
    delay(20)
    call(Lobby_EventScript_WalkInPlaceLastTalkedFacingPlayer)
    msgbox(format("Eso puede jugarme en contra."), MSGBOX_AUTOCLOSE, speaker = SP_NAME_GAMEBOY_CL)
    return
}

script Lobby_EventScript_Gameboycl_IntroRematch {
    msgbox(format("Kanto y Johto se unen en el mismo azul, el agua no recuerda fronteras.\p¿Quieres luchar contra uno de mis 3 equipos?"), MSGBOX_YESNO, speaker = SP_NAME_GAMEBOY_CL)
    closemessage
    delay(20)
    if(var(VAR_RESULT) == YES) {
        call(Lobby_EventScript_WalkInPlaceLastTalkedFacingPlayer)
        return
    } else {
        msgbox(format("Lo mejor de la vida no es competir, sino compartir."), MSGBOX_AUTOCLOSE, speaker = SP_NAME_GAMEBOY_CL)
        goto(Lobby_EventScript_Gameboycl_OnFinishEvent)
    }
}

script Lobby_EventScript_Gameboycl_OnFinishEvent {
    turnobject(VAR_LAST_TALKED, DIR_EAST)
    release
    end
}


script Lobby_EventScript_Gameboycl {
    lock
    faceplayer
    if(flag(FLAG_WAH_CHALLENGE_COMPLETED) == TRUE){
        if(flag(FLAG_DEFEATED_LOBBY_GAMEBOYCL)) {
            call(Lobby_EventScript_Gameboycl_IntroRematch)
        } else {
            call(Lobby_EventScript_Gameboycl_IntroFirstBattle)
        }
        delay(20)
        playse(SE_PIN)
        applymovementandwait(VAR_LAST_TALKED, Common_Movement_VersusWithDelay48)
        random(3)
        setflag(B_FLAG_NO_WHITEOUT)
        switch(var(VAR_RESULT)) {
            case 0:
                trainerbattle_no_intro(TRAINER_WAH_COLLABORATOR_GAMEBOY_CL_1, Lobby_Text_Gameboycl_OnDefeat)
            case 1:
                trainerbattle_no_intro(TRAINER_WAH_COLLABORATOR_GAMEBOY_CL_2, Lobby_Text_Gameboycl_OnDefeat)
            case 2:
                trainerbattle_no_intro(TRAINER_WAH_COLLABORATOR_GAMEBOY_CL_3, Lobby_Text_Gameboycl_OnDefeat)
        }
        clearflag(B_FLAG_NO_WHITEOUT)
        special(GetBattleOutcome)
        special(HealPlayerParty)
        if (var(VAR_RESULT) == B_OUTCOME_WON) {
            playse(SE_PIN)
            applymovement(VAR_LAST_TALKED, Common_Movement_HappyWithDelay48)
            msgbox(format("Definitivamente prefiero compartir por sobre competir."), MSGBOX_AUTOCLOSE, speaker = SP_NAME_GAMEBOY_CL)
            waitmovement(0)
            call(Lobby_EventScript_WalkInPlaceLastTalkedFacingPlayer)
            setflag(FLAG_DEFEATED_LOBBY_GAMEBOYCL)
        } else {
            msgbox(format("PENDIENTE GAMEBOY"), MSGBOX_AUTOCLOSE, speaker = SP_NAME_GAMEBOY_CL) //TODO Xiros
        }
        goto(Lobby_EventScript_Gameboycl_OnFinishEvent)
    }
    else {
        msgbox(format("Un hack rom es como la arquitectura, se construye sobre lo existente."), MSGBOX_AUTOCLOSE, speaker = SP_NAME_GAMEBOY_CL)
        call(Lobby_EventScript_WalkInPlaceLastTalkedFacingPlayer)
        msgbox(format("El arte aquí no es lo que se cambia, sino lo que se quiere transmitir."), MSGBOX_AUTOCLOSE, speaker = SP_NAME_GAMEBOY_CL)
        playse(SE_PIN)
        applymovementandwait(VAR_LAST_TALKED, Common_Movement_HeartWithDelay48)
        delay(20)
        goto(Lobby_EventScript_Gameboycl_OnFinishEvent)
    }
}

text Lobby_Text_Gameboycl_OnDefeat {
    format("Evolucionar también es mirar atrás.")
}

script Lobby_EventScript_FakemonDexMenu {
    lock
    faceplayer
    setvar(VAR_0x8000, 0)
    getseenmon(SPECIES_KAOSUNE)
    if(var(VAR_RESULT) != 0) {
        bufferspeciesname(STR_VAR_1, SPECIES_KAOSUNE)
        dynmultipush(gStringVar1, 0)
        addvar(VAR_0x8000, 1)
    }
    getseenmon(SPECIES_FREECH)
    if(var(VAR_RESULT) != 0) {
        bufferspeciesname(STR_VAR_1, SPECIES_FREECH)
        dynmultipush(gStringVar1, 1)
        addvar(VAR_0x8000, 1)
    }
    getseenmon(SPECIES_PLUGOINK)
    if(var(VAR_RESULT) != 0) {
        bufferspeciesname(STR_VAR_1, SPECIES_PLUGOINK)
        dynmultipush(gStringVar1, 2)
        addvar(VAR_0x8000, 1)
    }
    getseenmon(SPECIES_GRITESPEC)
    if(var(VAR_RESULT) != 0) {
        bufferspeciesname(STR_VAR_1, SPECIES_GRITESPEC)
        dynmultipush(gStringVar1, 3)
        addvar(VAR_0x8000, 1)
    }
    getseenmon(SPECIES_MAGIKEN)
    if(var(VAR_RESULT) != 0) {
        bufferspeciesname(STR_VAR_1, SPECIES_MAGIKEN)
        dynmultipush(gStringVar1, 4)
        addvar(VAR_0x8000, 1)
    }
    getseenmon(SPECIES_AYEWIRAZ)
    if(var(VAR_RESULT) != 0) {
        bufferspeciesname(STR_VAR_1, SPECIES_AYEWIRAZ)
        dynmultipush(gStringVar1, 5)
        addvar(VAR_0x8000, 1)
    }
    getseenmon(SPECIES_ELECTRODES)
    if(var(VAR_RESULT) != 0) {
        bufferspeciesname(STR_VAR_1, SPECIES_ELECTRODES)
        dynmultipush(gStringVar1, 6)
        addvar(VAR_0x8000, 1)
    }
    getseenmon(SPECIES_EING)
    if(var(VAR_RESULT) != 0) {
        bufferspeciesname(STR_VAR_1, SPECIES_EING)
        dynmultipush(gStringVar1, 7)
        addvar(VAR_0x8000, 1)
    }
    if(var(VAR_0x8000) == 0) {
        msgbox(Lobby_Text_FakemonDex_NoneSeen)
        release
        end
    }
    dynmultistack(0, 0, FALSE, 6, 0, 0, DYN_MULTICHOICE_CB_NONE)
    if(var(VAR_RESULT) > 7) {
        release
        end
    }
    switch(var(VAR_RESULT)) {
        case 0:
            msgbox(Lobby_Text_Fakemon_Kaosune)
        case 1:
            msgbox(Lobby_Text_Fakemon_Freech)
        case 2:
            msgbox(Lobby_Text_Fakemon_Plugoink)
        case 3:
            msgbox(Lobby_Text_Fakemon_Gritespec)
        case 4:
            msgbox(Lobby_Text_Fakemon_Magiken)
        case 5:
            msgbox(Lobby_Text_Fakemon_Ayewiraz)
        case 6:
            msgbox(Lobby_Text_Fakemon_Electrodes)
        case 7:
            msgbox(Lobby_Text_Fakemon_Eing)
    }
    release
    end
}

text Lobby_Text_FakemonDex_NoneSeen {
    "Aún no has visto ningún fakemon en tu Pokédex."
}

text Lobby_Text_Fakemon_Kaosune {
    "Información sobre Kaosune (placeholder)."
}

text Lobby_Text_Fakemon_Freech {
    "Información sobre Freech (placeholder)."
}

text Lobby_Text_Fakemon_Plugoink {
    "Información sobre Plugoink (placeholder)."
}

text Lobby_Text_Fakemon_Gritespec {
    "Información sobre Gritespec (placeholder)."
}

text Lobby_Text_Fakemon_Magiken {
    "Información sobre Magiken (placeholder)."
}

text Lobby_Text_Fakemon_Ayewiraz {
    "Información sobre Ayewiraz (placeholder)."
}

text Lobby_Text_Fakemon_Electrodes {
    "Información sobre Electrodes (placeholder)."
}

text Lobby_Text_Fakemon_Eing {
    "Información sobre Eing (placeholder)."
}