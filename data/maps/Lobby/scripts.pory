mapscripts Lobby_MapScripts {
    MAP_SCRIPT_ON_TRANSITION: Lobby_OnTransition
}

script Lobby_OnTransition {
    setrespawn(HEAL_LOCATION_LOBBY)
    end
}

script Lobby_EventScript_StartChallenge {
    lock
    applymovementandwait(LOCALID_LOBBY_POLICE, Common_Movement_FacePlayer)
    msgbox(format("Pendiente textos, ¿empezar desafío?"), MSGBOX_YESNO)
    if(var(VAR_RESULT) == YES) {
        warp(MAP_OMEGA_ROOM, WARP_ID_OMEGA_ROOM_INITIAL)
        waitstate
    } else {
        applymovementandwait(LOCALID_PLAYER, Common_Movement_WalkDown)
        turnobject(LOCALID_LOBBY_POLICE, DIR_SOUTH)
    }
    release
    end
}


script Lobby_EventScript_TeamSelector {
    lock
    faceplayer
    msgbox(format("¡Hola! Soy el encargado de la selección de equipos para el desafío.\pVeo que equipo actual es bueno, pero creo que puedes conseguir uno mejor.\p¿Quieres elegir otro equipo?"), MSGBOX_YESNO)
    if(var(VAR_RESULT) == YES) {
        special(OpenTeamSelectorFromField)
        waitstate
        if(var(VAR_RESULT) == TRUE) {
            msgbox( format("¡Excelente elección! Eso sí es un equipo ganador.\p¡Ve por la gloria!"), MSGBOX_AUTOCLOSE)
        } else {
            msgbox(Lobby_Text_TeamSelector_TeamSelectionCancelled, MSGBOX_AUTOCLOSE)
        }
    } else {
        msgbox(Lobby_Text_TeamSelector_TeamSelectionCancelled, MSGBOX_AUTOCLOSE)
    }
    turnobject(VAR_LAST_TALKED, DIR_SOUTH)
    release
    end
}

script Lobby_EventScript_SeenMonDetails {
    lock
    faceplayer
    setvar(VAR_0x8000, 0)
    getseenmon(SPECIES_ELECTRODES)
    if(var(VAR_RESULT) == TRUE) {
        dynmultipush(Lobby_Text_SeenMon_ElectrodesOption, 0)
        addvar(VAR_0x8000, 1)
    }
    getseenmon(SPECIES_PLUGOINK)
    if(var(VAR_RESULT) == TRUE) {
        dynmultipush(Lobby_Text_SeenMon_PlugoinkOption, 1)
        addvar(VAR_0x8000, 1)
    }
    getseenmon(SPECIES_GRITESPEC)
    if(var(VAR_RESULT) == TRUE) {
        dynmultipush(Lobby_Text_SeenMon_GritespecOption, 2)
        addvar(VAR_0x8000, 1)
    }
    if(var(VAR_0x8000) == 0) {
        msgbox(Lobby_Text_SeenMon_NoOptions, MSGBOX_AUTOCLOSE)
        release
        end
    }
    dynmultistack(0, 0, FALSE, 3, FALSE, 0, DYN_MULTICHOICE_CB_NONE)
    switch VAR_RESULT
    case 0, Lobby_EventScript_SeenMon_Electrodes
    case 1, Lobby_EventScript_SeenMon_Plugoink
    case 2, Lobby_EventScript_SeenMon_Gritespec
    release
    end
}

script Lobby_EventScript_SeenMon_Electrodes {
    msgbox(Lobby_Text_SeenMon_ElectrodesDetail, MSGBOX_AUTOCLOSE)
    return
}

script Lobby_EventScript_SeenMon_Plugoink {
    msgbox(Lobby_Text_SeenMon_PlugoinkDetail, MSGBOX_AUTOCLOSE)
    return
}

script Lobby_EventScript_SeenMon_Gritespec {
    msgbox(Lobby_Text_SeenMon_GritespecDetail, MSGBOX_AUTOCLOSE)
    return
}


text Lobby_Text_TeamSelector_TeamSelectionCancelled {
    "¡Vaya, vaya! Un{A} chic{OA} valiente, ¿eh?\nAsí que piensas ganar con ESE equipo…\p¡Buena suerte, la vas a necesitar!"
}

text Lobby_Text_SeenMon_ElectrodesOption {
    "Electrodes"
}

text Lobby_Text_SeenMon_PlugoinkOption {
    "Plug-Oink"
}

text Lobby_Text_SeenMon_GritespecOption {
    "Gritespec"
}

text Lobby_Text_SeenMon_NoOptions {
    "Aún no has visto ninguno de estos Pokémon."
}

text Lobby_Text_SeenMon_ElectrodesDetail {
    "Electrodes: un Pokémon eléctrico lleno de energía."
}

text Lobby_Text_SeenMon_PlugoinkDetail {
    "Plug-Oink: carga electricidad mientras corre sin parar."
}

text Lobby_Text_SeenMon_GritespecDetail {
    "Gritespec: te observa desde las sombras con curiosidad."
}


script Lobby_EventScript_Shop {
	lock
    faceplayer
	message(format("¡Hola! ¿En qué puedo ayudarte?"))
	waitmessage
	pokemart(MyMartItems)
	msgbox(format("Gracias por tu visita. ¡Que tengas un buen día!"), MSGBOX_AUTOCLOSE)
	release
    end
}

mart MyMartItems {
	ITEM_HYPER_POTION
	ITEM_MAX_POTION
    ITEM_FULL_RESTORE
	ITEM_ETHER
    ITEM_MAX_ETHER
    ITEM_ELIXIR
    ITEM_MAX_ELIXIR
    ITEM_REVIVE
    ITEM_MAX_REVIVE
    ITEM_REVIVAL_HERB
     //TODO Xiros ITEM_BAXCALIBURITE
    ITEM_DRAGALGITE
    ITEM_TYRANITARITE
    //TODO Xiros ITEM_DARKRAINITE
    ITEM_ALAKAZITE
    ITEM_LUCARIONITE
     //TODO Xiros ITEM_LUCARIONITE_Z
    ITEM_MEWTWONITE_X
    ITEM_MEWTWONITE_Y
    ITEM_CHARIZARDITE_X
    ITEM_CHARIZARDITE_Y
    ITEM_GALLADITE
    ITEM_GLALITITE
    ITEM_FROSLASSITE
    ITEM_CLEFABLITE
    ITEM_HOUNDOOMINITE
    ITEM_AGGRONITE
    ITEM_ZYGARDITE
    ITEM_VICTREEBELITE
}
