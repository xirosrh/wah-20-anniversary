mapscripts SayerBlaxRoom_MapScripts {
    MAP_SCRIPT_ON_FRAME_TABLE [
		VAR_SAYER_BLAX_ROOM_STATE, 0: SayerBlaxRoom_OnFrame
	]
}

script SayerBlaxRoom_OnFrame {
    lock
    applymovement(LOCALID_PLAYER, Common_Movement_WalkUp3)
    waitmovement(0)
    special(SpawnCameraObject)
    directcameramovementtopos(6,5)
    waitmovement(0)
    applymovement(LOCALID_SAYERBLAXROOM_BLAX, Common_Movement_WalkLeft)
    waitmovement(0)
    msgbox(format("Fuaaah, ¡qué cansancio! Con lo que me hacía falta descansar...\p¿Ya es el día del aniversario?"), MSGBOX_AUTOCLOSE)
    applymovement(LOCALID_SAYERBLAXROOM_BLAX, moves(
        walk_left
        walk_down
        walk_left
        face_down
        delay_16))
    waitmovement(0)
    playse(SE_PIN)
    applymovement(LOCALID_SAYERBLAXROOM_BLAX, moves(
        emote_question_mark
        jump_in_place_down * 2
        delay_16
    ))
    waitmovement(0)
    msgbox(format("¿Tú quién eres?"), MSGBOX_AUTOCLOSE)
    applymovement(LOCALID_SAYERBLAXROOM_BLAX, Common_Movement_WalkDown4)
    applymovement(LOCALID_CAMERA, Common_Movement_WalkDown5)
    waitmovement(LOCALID_CAMERA)
    msgbox(format("¿Cómo, que quieres combatir? Yo me jubilé hace muchos años chaval.\pHemos pasado una pandemia incluso... Pero si insistes... No creas que te lo pondré fácil."), MSGBOX_AUTOCLOSE)
    delay(20)
    applymovement(LOCALID_SAYERBLAXROOM_BLAX, moves(
        face_up
        delay_16
        face_right
        delay_16
        face_left
        delay_16
    ))
    waitmovement(0)
    msgbox(format("¿Y ahora dónde se ha metido Sayer…? Se supone nos tocaba combatir a los dos."), MSGBOX_AUTOCLOSE)
    applymovement(LOCALID_SAYERBLAXROOM_SAYER, moves(
        jump_2_right * 2
        jump_right
    ))
    waitmovement(0)
    msgbox(format("Welcome to the jungle!"))
    special(RemoveCameraObject)
    trainerbattle_two_trainers(TRAINER_WAH_ADMIN_GOCE_MAIN, "Perdi blax", TRAINER_WAH_ADMIN_OMEGA_MAIN, "Perdi sayer")
    setvar(VAR_SAYER_BLAX_ROOM_STATE, 1)
    release
    end

}