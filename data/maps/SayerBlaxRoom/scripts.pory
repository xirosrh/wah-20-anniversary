mapscripts SayerBlaxRoom_MapScripts {
    MAP_SCRIPT_ON_FRAME_TABLE [
		VAR_SAYER_BLAX_ROOM_STATE, 0: SayerBlaxRoom_OnFrame
	]
}

script SayerBlaxRoom_OnFrame {
    applymovementandwait(LOCALID_PLAYER, Common_Movement_WalkUp3)
    special(SpawnCameraObject)
    directcameramovementtopos(6,5)
    waitmovement(0)
    delay(200)
    setobjectmovementtype2(LOCALID_SAYERBLAXROOM_BLAX, MOVEMENT_TYPE_FACE_DOWN)
    turnobject(LOCALID_SAYERBLAXROOM_BLAX, DIR_SOUTH)
    delay(30)
    msgbox(format("Fuaaah, ¡qué cansancio! Con lo que me hacía falta descansar…"), MSGBOX_AUTOCLOSE)
    applymovementandwait(LOCALID_SAYERBLAXROOM_BLAX, Common_Movement_WalkLeft)
    waitmovement(0)
    msgbox(format("¿Ya es el día del aniversario?"), MSGBOX_AUTOCLOSE)
    applymovementandwait(LOCALID_SAYERBLAXROOM_BLAX, moves(
        walk_left
        walk_down
        walk_left
        face_down
        delay_16))
    playse(SE_PIN)
    applymovementandwait(LOCALID_SAYERBLAXROOM_BLAX, moves(
        emote_question_mark
        jump_in_place_down * 2
        delay_16
    ))
    msgbox(format("¿Tú quién eres?"), MSGBOX_AUTOCLOSE)
    applymovement(LOCALID_SAYERBLAXROOM_BLAX, Common_Movement_WalkDown4)
    applymovement(LOCALID_CAMERA, Common_Movement_WalkDown5)
    waitmovement(LOCALID_CAMERA)
    msgbox(format("¿Cómo, que quieres combatir? Yo me jubilé hace muchos años chaval.\pHemos pasado una pandemia incluso… Pero si insistes… No creas que te lo pondré fácil."), MSGBOX_AUTOCLOSE)
    delay(20)
    applymovementandwait(LOCALID_SAYERBLAXROOM_BLAX, moves(
        face_up
        delay_16
        face_right
        delay_16
        face_left
        delay_16
    ))
    msgbox(format("¿Y ahora dónde se ha metido Sayer…? Se supone nos tocaba combatir a los dos."), MSGBOX_AUTOCLOSE)
    applymovementandwait(LOCALID_SAYERBLAXROOM_SAYER, moves(
        jump_2_right * 2
        jump_right
    ))
    turnobject(LOCALID_PLAYER, DIR_WEST)
    msgbox(format("Welcome to the jungle!"))
    special(RemoveCameraObject)
    turnobject(LOCALID_SAYERBLAXROOM_BLAX, DIR_SOUTH)
    playse(SE_PIN)
    applymovement(LOCALID_SAYERBLAXROOM_BLAX, Common_Movement_VersusWithDelay48)
    applymovement(LOCALID_SAYERBLAXROOM_SAYER, Common_Movement_VersusWithDelay48)
    waitmovement(0)
    // Blax is admin index 3, Sayer is admin index 4
    if (!flag(FLAG_WAH_CHALLENGE_COMPLETED)) {
        trainerbattle_two_trainers(TRAINER_WAH_ADMIN_BLAX_MAIN, format("Bueno, pues has ganado. ¡Enhorabuena!"), TRAINER_WAH_ADMIN_SAYER_MAIN, "Pues si que la has liado.")
    } else {
        setvar(VAR_0x8004, 3)
        special(ShouldUseAlternativeTeam)
        copyvar(VAR_0x8005, VAR_RESULT)
        setvar(VAR_0x8004, 4)
        special(ShouldUseAlternativeTeam)
        // VAR_0x8005 = Blax team, VAR_RESULT = Sayer team
        if (var(VAR_0x8005) == 0) {
            if (var(VAR_RESULT) == 0) {
                trainerbattle_two_trainers(TRAINER_WAH_ADMIN_BLAX_MAIN, format("Bueno, pues has ganado. ¡Enhorabuena!"), TRAINER_WAH_ADMIN_SAYER_MAIN, "Pues si que la has liado.")
            } else {
                trainerbattle_two_trainers(TRAINER_WAH_ADMIN_BLAX_MAIN, format("Bueno, pues has ganado. ¡Enhorabuena!"), TRAINER_WAH_ADMIN_SAYER_ALTERNATIVE, "Pues si que la has liado.")
            }
        } else {
            if (var(VAR_RESULT) == 0) {
                trainerbattle_two_trainers(TRAINER_WAH_ADMIN_BLAX_ALTERNATIVE, format("Bueno, pues has ganado. ¡Enhorabuena!"), TRAINER_WAH_ADMIN_SAYER_MAIN, "Pues si que la has liado.")
            } else {
                trainerbattle_two_trainers(TRAINER_WAH_ADMIN_BLAX_ALTERNATIVE, format("Bueno, pues has ganado. ¡Enhorabuena!"), TRAINER_WAH_ADMIN_SAYER_ALTERNATIVE, "Pues si que la has liado.")
            }
        }
    }
    applymovement(LOCALID_SAYERBLAXROOM_SAYER, moves(
        emote_x
        lock_facing_direction
        jump_2_left
        unlock_facing_direction
    ))
    applymovement(LOCALID_SAYERBLAXROOM_BLAX, moves(
        emote_x
        lock_facing_direction
        jump_2_up
        unlock_facing_direction
    ))
    waitmovement(0)
    delay(20)
    applymovementandwait(LOCALID_SAYERBLAXROOM_BLAX, Common_Movement_WalkDown2)
    turnobject(LOCALID_PLAYER, DIR_NORTH)
    msgbox(format("Ha sido un combate espectacular. Confirmamos que estoy más oxidado de lo que pensaba."), MSGBOX_DEFAULT)
    playse(SE_PIN)
    applymovementandwait(LOCALID_SAYERBLAXROOM_BLAX, Common_Movement_SweatDropWithDelay48)
    msgbox(format("Todavía te quedan muchos combates por delante, pero aprovecha cada uno de ellos para celebrar estos 20 años del mejor foro de ROM Hacking y fangaming de Pokémon del mundo.\pDicho esto… Me vuelvo a dormir. ¡Mucha suerte!"), MSGBOX_AUTOCLOSE)
    applymovementandwait(LOCALID_SAYERBLAXROOM_BLAX, moves(
        walk_up * 5
        walk_right * 3
        face_down
    ))
    setobjectmovementtype2(LOCALID_SAYERBLAXROOM_BLAX, MOVEMENT_TYPE_SLEEPING)
    applymovementandwait(LOCALID_SAYERBLAXROOM_SAYER, Common_Movement_WalkRight2)
    turnobject(LOCALID_PLAYER, DIR_WEST)
    msgbox(format("Y recuerda: No la líes y si puedes, pues si."), MSGBOX_AUTOCLOSE)
    setvar(VAR_SAYER_BLAX_ROOM_STATE, 1)
    call(PokemonLeague_EliteFour_SetAdvanceToNextRoomMetatiles) //TODO Xiros
    setflag(FLAG_DEFEATED_ADMINS_SAYER_AND_BLAX)
    release
    end

}

script SayerBlaxRoom_EventScript_Blax {
    msgbox(format("Zzz… ¡Ve, BLAxTOISE!\pZzz…"), MSGBOX_AUTOCLOSE)
    end
}

script SayerBlaxRoom_EventScript_Sayer {
    msgbox(format("No me puedo quejar la verdad, quedé bastante contento con la batalla.\pAvanza, fiera."), MSGBOX_AUTOCLOSE)
    end
}