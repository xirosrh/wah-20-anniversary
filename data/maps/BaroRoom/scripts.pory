mapscripts BaroRoom_MapScripts {
    MAP_SCRIPT_ON_FRAME_TABLE [
        VAR_BARO_ROOM_STATE, 0: BaroRoom_OnFrame
	]
}

script BaroRoom_OnFrame {
    applymovementandwait(LOCALID_PLAYER, moves(
        walk_up * 7, 
        face_right
        delay_16
        face_left
        delay_16
        face_up
        delay_16
        face_down
        delay_16))
    playse(SE_PIN)
    applymovementandwait(LOCALID_PLAYER, Common_Movement_QuestionMarkWithDelay48)
    special(SpawnCameraObject)
    applymovementandwait(LOCALID_CAMERA, Common_Movement_WalkRight3)
    waitmovement(0)
    call(BaroRoom_EventScript_OpenSideDoor)
    showobjectat(LOCALID_BAROROOM_BARO, MAP_BARO_ROOM)
    setobjectmovementtype(LOCALID_BAROROOM_BARO, MOVEMENT_TYPE_FACE_LEFT)
    setobjectmovementtype2(LOCALID_BAROROOM_BARO, MOVEMENT_TYPE_FACE_LEFT)
    applymovementandwait(LOCALID_BAROROOM_BARO, Common_Movement_WalkInPlaceLeft)
    applymovementandwait(LOCALID_PLAYER, Common_Movement_FaceRight)
    playse(SE_PIN)
    applymovementandwait(LOCALID_PLAYER, Common_Movement_ExclamationMarkWithDelay48)
    delay(10)
    msgbox(format("Erhm… Tú eres {PLAYER}, ¿no? Encantado, soy Baro."), MSGBOX_AUTOCLOSE)
    applymovementandwait(LOCALID_BAROROOM_BARO, Common_Movement_WalkLeft)
    call(BaroRoom_EventScript_CloseSideDoor)
    applymovement(LOCALID_CAMERA, Common_Movement_WalkLeft3)
    applymovement(LOCALID_BAROROOM_BARO, Common_Movement_WalkLeft3)
    waitmovement(0)
    applymovementandwait(LOCALID_BAROROOM_BARO, moves(walk_up, walk_left *2, face_down))
    applymovementandwait(LOCALID_PLAYER, Common_Movement_FaceUp)
    special(RemoveCameraObject)
    msgbox(format("En fin… En qué jaleos se mete uno… "), MSGBOX_AUTOCLOSE)
    playse(SE_PIN)
    applymovementandwait(LOCALID_BAROROOM_BARO, Common_Movement_SweatDropWithDelay48)
    msgbox(format("¿Yo estaba en una comunidad? ¿En serio? ¿De Pokémon?"))
    playse(SE_PIN)
    applymovement(LOCALID_BAROROOM_BARO, moves(delay_16, emote_surprise, delay_16 * 3))
    msgbox(format("Wow."))
    waitmovement(0)
    msgbox(format("Vaya tiempos… En fin."), MSGBOX_AUTOCLOSE)
    delay(20)
    msgbox(format("Supongo que lo de reminiscir del pasado es apropiado para el momento. Tu viaje y todo eso.\pEn fin, nada, que me han pedido que me pase por aquí a hacer una peleilla contigo. ¿Vamos?"))
    applymovementandwait(LOCALID_BAROROOM_BARO, Common_Movement_WalkInPlaceDown)
    trainerbattle_no_intro(TRAINER_WAH_ADMIN_BARO_MAIN, format("En fin…"))
    msgbox(format("El Pikachu no te lo esperabas, ¿eh?"), MSGBOX_AUTOCLOSE)
    playse(SE_PIN)
    applymovementandwait(LOCALID_BAROROOM_BARO, Common_Movement_HappyWithDelay48)
    msgbox(format("Pues ya estaría. Ya he cumplido. Chao."), MSGBOX_AUTOCLOSE)
    special(SpawnCameraObject)
    applymovementandwait(LOCALID_BAROROOM_BARO, moves(
        walk_right * 2, walk_down
    ))
    applymovement(LOCALID_CAMERA, Common_Movement_WalkRight3)
    applymovement(LOCALID_BAROROOM_BARO, Common_Movement_WalkRight3)
    waitmovement(0)

    special(RemoveCameraObject)
    setvar(VAR_BARO_ROOM_STATE, 1)
    setflag(FLAG_DEFEATED_ADMIN_BARO)
    release
    end
 }


//TODO Xiros: Update this when real tiles are inserted
 script BaroRoom_EventScript_OpenSideDoor {
    setmetatile(12, 6, METATILE_EliteFour_SideDoor_Open, FALSE)
	special(DrawWholeMapView)
    return
 }

//TODO Xiros: Update this when real tiles are inserted
  script BaroRoom_EventScript_CloseSideDoor {
    setmetatile(12, 6, METATILE_EliteFour_SideDoor_Closed, FALSE)
	special(DrawWholeMapView)
    return
 }