const VAR_TEMP_A_TOKYN_WAR_ROOM_STATE = VAR_TEMP_A

mapscripts TokynWarRoom_MapScripts {
    MAP_SCRIPT_ON_WARP_INTO_MAP_TABLE [
        VAR_TEMP_8, 0: TokynWarRoom_OnWarp_PlayerTurnNorth
    ]
    MAP_SCRIPT_ON_LOAD: TokynWarRoom_OnLoad
}


script TokynWarRoom_OnLoad {
    if(flag(FLAG_DEFEATED_ADMINS_TOKYN_AND_WAR)){
        call(WahRoomsShared_SetDoorAsOpen)
    }
    end
}

script TokynWarRoom_OnWarp_PlayerTurnNorth {
    turnobject(LOCALID_PLAYER, DIR_NORTH)
    end
}

script TokynWarRoom_EventScript_MainCutscene {
    lockall
    directmovementtopos(LOCALID_PLAYER, 6, 7)
    waitmovement(0)
    turnobject(LOCALID_PLAYER, DIR_NORTH)
    playse(SE_PIN)
    applymovementandwait(LOCALID_TOKYNWARROOM_TOKYN, Common_Movement_SurpriseWithDelay48)
    applymovementandwait(LOCALID_TOKYNWARROOM_TOKYN, moves(walk_down * 2, face_right))
    turnobject(LOCALID_PLAYER, DIR_WEST)
    msgbox(format("¡Vaya sorpresa! No esperaba ver una cara nueva por aquí."), MSGBOX_AUTOCLOSE, speaker = SP_NAME_TOKYN)
    applymovementandwait(LOCALID_TOKYNWARROOM_TOKYN, Common_Movement_WalkInPlaceRight)
    applymovementandwait(LOCALID_TOKYNWARROOM_WAR, moves(walk_down * 2, face_left))
    turnobject(LOCALID_PLAYER, DIR_EAST)
    msgbox(format("Yo tampoco tengo idea de quién eres, pero eso no importa."), speaker = SP_NAME_WAR)
    playse(SE_PIN)
    applymovement(LOCALID_TOKYNWARROOM_WAR, moves(emote_exclamation_mark))
    waitmovement(0)
    waitse
    playse(SE_LEDGE)
    applymovement(LOCALID_TOKYNWARROOM_WAR, moves(jump_in_place_left))
    waitmovement(0)
    waitse
    playse(SE_LEDGE)
    applymovement(LOCALID_TOKYNWARROOM_WAR, moves(jump_in_place_left, delay_16))
    waitmovement(0)
    waitse
    msgbox(format("¡Estamos aquí para verlo!"), MSGBOX_AUTOCLOSE, speaker = SP_NAME_WAR)
    waitmovement(0)
    applymovementandwait(LOCALID_TOKYNWARROOM_TOKYN, Common_Movement_WalkInPlaceRight)
    turnobject(LOCALID_PLAYER, DIR_WEST)
    msgbox(format("Es un placer poder participar de esta celebración contigo."),MSGBOX_AUTOCLOSE, speaker = SP_NAME_TOKYN)
    playse(SE_PIN)
    applymovementandwait(LOCALID_TOKYNWARROOM_TOKYN, Common_Movement_HappyWithDelay48)
    applymovementandwait(LOCALID_TOKYNWARROOM_TOKYN, Common_Movement_WalkInPlaceRight)
    msgbox(format("¿Qué te parece un combate de a dos, War?"), MSGBOX_AUTOCLOSE, speaker = SP_NAME_TOKYN)
    playse(SE_LEDGE)
    applymovement(LOCALID_TOKYNWARROOM_WAR, moves(jump_in_place_left))
    waitmovement(0)
    waitse
    playse(SE_LEDGE)
    applymovement(LOCALID_TOKYNWARROOM_WAR, moves(jump_in_place_left))
    waitmovement(0)
    waitse
    turnobject(LOCALID_PLAYER, DIR_EAST)
    msgbox(format("¿Unir fuerzas como en los viejos tiempos?"), MSGBOX_AUTOCLOSE, speaker = SP_NAME_WAR)
    delay(30)
    applymovementandwait(LOCALID_TOKYNWARROOM_WAR, Common_Movement_WalkRight)
    playse(SE_PIN)
    applymovementandwait(LOCALID_TOKYNWARROOM_WAR, Common_Movement_ThinkingWithDelay48)
    msgbox(format("¡Hace años no hacemos eso! Pero me gusta. Una buena celebración merece algo especial."), MSGBOX_AUTOCLOSE, speaker = SP_NAME_WAR)
    applymovementandwait(LOCALID_TOKYNWARROOM_WAR, Common_Movement_WalkInPlaceLeft)
    delay(20)
    applymovementandwait(LOCALID_TOKYNWARROOM_TOKYN, Common_Movement_WalkInPlaceRight)
    turnobject(LOCALID_PLAYER, DIR_WEST)
    msgbox(format("Entonces está decidido."), MSGBOX_AUTOCLOSE, speaker = SP_NAME_TOKYN)
    delay(30)
    applymovementandwait(LOCALID_TOKYNWARROOM_TOKYN, Common_Movement_WalkInPlaceRight)
    msgbox(format("¡Vamos!"), MSGBOX_AUTOCLOSE, speaker = SP_NAME_TOKYN)
    delay(20)
    applymovementandwait(LOCALID_TOKYNWARROOM_WAR, Common_Movement_WalkLeft)
    turnobject(LOCALID_PLAYER, DIR_EAST)
    msgbox(format("¡Que gane el mejor!"), MSGBOX_AUTOCLOSE, speaker = SP_NAME_WAR)
    playse(SE_PIN)
    applymovement(LOCALID_TOKYNWARROOM_TOKYN, moves(emote_versus, jump_in_place_right * 2, delay_16))
    applymovement(LOCALID_TOKYNWARROOM_WAR, moves(emote_versus, jump_in_place_left * 2, delay_16))
    applymovement(LOCALID_PLAYER, moves(face_left, delay_8, face_right, delay_8, face_left, delay_8, face_right, delay_8))
    waitmovement(LOCALID_TOKYNWARROOM_WAR)
    call(TokynWarRoom_EventScript_TokynWarBattle)
    playse(SE_PIN)
    applymovement(LOCALID_TOKYNWARROOM_TOKYN, moves(emote_x, lock_facing_direction))
    applymovement(LOCALID_TOKYNWARROOM_WAR, moves(emote_x, lock_facing_direction))
    waitmovement(0)
    waitse
    playse(SE_LEDGE)
    applymovement(LOCALID_TOKYNWARROOM_TOKYN, moves(jump_left, unlock_facing_direction, delay_16 * 2))
    applymovement(LOCALID_TOKYNWARROOM_WAR, moves(jump_right, unlock_facing_direction, delay_16 * 2))
    waitmovement(0)
    waitse
    playse(SE_PIN)
    applymovementandwait(LOCALID_TOKYNWARROOM_WAR, moves(emote_double_exclamation_mark))
    waitse
    playse(SE_LEDGE)
    applymovementandwait(LOCALID_TOKYNWARROOM_WAR, moves(jump_in_place_left))
    waitse
    playse(SE_LEDGE)
    applymovementandwait(LOCALID_TOKYNWARROOM_WAR, moves(jump_in_place_left, walk_left))
    waitse
    msgbox(format("¡Pero qué locura de combate! Hoy sí que nos diste trabajo en serio."), MSGBOX_AUTOCLOSE, speaker = SP_NAME_WAR)
    delay(40)
    applymovementandwait(LOCALID_TOKYNWARROOM_TOKYN, Common_Movement_WalkRight)
    turnobject(LOCALID_PLAYER, DIR_WEST)
    msgbox(format("Gracias por compartir este momento con nosotros. Fue un encuentro digno para esta celebración.\pEs increíble cómo un solo combate puede traer de vuelta tantas memorias."), MSGBOX_AUTOCLOSE, speaker = SP_NAME_TOKYN)
    playse(SE_PIN)
    applymovementandwait(LOCALID_TOKYNWARROOM_TOKYN, Common_Movement_HeartWithDelay48)
    call(WahRoomsShared_OpenDoor)
    msgbox(format("Ya puedes seguir, pero recuerda: Nunca olvides seguir tus sueños."), MSGBOX_AUTOCLOSE, speaker = SP_NAME_TOKYN)
    applymovement(LOCALID_TOKYNWARROOM_TOKYN, moves(walk_up * 2, face_down))
    applymovement(LOCALID_TOKYNWARROOM_WAR, moves(walk_up * 2, face_down))
    waitmovement(0)
    setflag(FLAG_DEFEATED_ADMINS_TOKYN_AND_WAR)
    setvar(VAR_TEMP_A_TOKYN_WAR_ROOM_STATE, 2)
    releaseall
    return
}

script TokynWarRoom_EventScript_War {
    lock
    faceplayer
    if(var(VAR_TEMP_A_TOKYN_WAR_ROOM_STATE) == 0) {
        release
        call(TokynWarRoom_EventScript_MainCutscene)
        end
    }
    msgbox(format("¿Quieres otra? ¡Ni loco!\pTokyn me tira charla motivadora si pierdo dos veces seguidas.\pPero estuvo buenísimo, eh."), speaker = SP_NAME_WAR)
    playse(SE_PIN)
    applymovementandwait(LOCALID_TOKYNWARROOM_WAR, Common_Movement_XDWithDelay48)
    release
    end
}

script TokynWarRoom_EventScript_Tokyn {
    lock
    faceplayer
    if(var(VAR_TEMP_A_TOKYN_WAR_ROOM_STATE) == 0) {
        release
        call(TokynWarRoom_EventScript_MainCutscene)
        end
    }
    msgbox(format("Me alegra muchísimo haber sido convocado para esta celebración.\pEstar acá, compartir un combate así… me trajo muy buenos recuerdos."), speaker = SP_NAME_TOKYN)
    playse(SE_PIN)
    applymovementandwait(LOCALID_TOKYNWARROOM_TOKYN, Common_Movement_HeartWithDelay48)
    msgbox(format("Gracias por hacerlo especial."), speaker = SP_NAME_TOKYN)
    release
    end
}


script TokynWarRoom_EventScript_MagikarpJumping {
    lock
    getplayerxy(VAR_TEMP_0, VAR_TEMP_1)
    playse(SE_BALLOON_RED)
    switch(var(VAR_TEMP_0)){
        case 5:
            load_field_pic(FP_MAGIKARP_JUMPING_LEFT, 102, 60, VAR_TEMP_2)
            load_field_pic(FP_MAGIKARP_JUMPING_RIGHT, 166, 60, VAR_TEMP_3)
        case 6:
            load_field_pic(FP_MAGIKARP_JUMPING_LEFT, 86, 60, VAR_TEMP_2)
            load_field_pic(FP_MAGIKARP_JUMPING_RIGHT, 150, 60, VAR_TEMP_3)
        case 7:
            load_field_pic(FP_MAGIKARP_JUMPING_LEFT, 70, 60, VAR_TEMP_2)
            load_field_pic(FP_MAGIKARP_JUMPING_RIGHT, 134, 60, VAR_TEMP_3)
    }
    waitse
    playse(SE_M_DIVE)
    delay(70)
    destroy_field_pic(FP_MAGIKARP_JUMPING_LEFT, VAR_TEMP_2)
    destroy_field_pic(FP_MAGIKARP_JUMPING_RIGHT, VAR_TEMP_3)
    waitse
    turnobject(LOCALID_PLAYER, DIR_WEST)
    playse(SE_PIN)
    applymovementandwait(LOCALID_PLAYER, Common_Movement_DoubleExclamationMarkWithDelay48)
    setvar(VAR_TEMP_A_TOKYN_WAR_ROOM_STATE, 1)
    release
    end
}

script TokynWarRoom_EventScript_TokynWarBattle {
    if (!flag(FLAG_WAH_CHALLENGE_COMPLETED)) {
        trainerbattle_two_trainers(TRAINER_WAH_ADMIN_TOKYN_MAIN, TokynWarRoom_Text_TokynDefeated, TRAINER_WAH_ADMIN_WAR_MAIN, TokynWarRoom_Text_WarDefeated)
    } else {
        setvar(VAR_0x8004, TEAM_INDEX_TOKYN)
        special(ShouldUseAlternativeTeam)
        copyvar(VAR_0x8005, VAR_RESULT)
        setvar(VAR_0x8004, TEAM_INDEX_WAR)
        special(ShouldUseAlternativeTeam)
        // VAR_0x8005 = Tokyn team, VAR_RESULT = War team
        if (var(VAR_0x8005) == 0) {
            if (var(VAR_RESULT) == 0) {
                trainerbattle_two_trainers(TRAINER_WAH_ADMIN_TOKYN_MAIN, TokynWarRoom_Text_TokynDefeated, TRAINER_WAH_ADMIN_WAR_MAIN, TokynWarRoom_Text_WarDefeated)
            } else {
                trainerbattle_two_trainers(TRAINER_WAH_ADMIN_TOKYN_MAIN, TokynWarRoom_Text_TokynDefeated, TRAINER_WAH_ADMIN_WAR_ALTERNATIVE, TokynWarRoom_Text_WarDefeated)
            }
        } else {
            if (var(VAR_RESULT) == 0) {
                trainerbattle_two_trainers(TRAINER_WAH_ADMIN_TOKYN_ALTERNATIVE, TokynWarRoom_Text_TokynDefeated, TRAINER_WAH_ADMIN_WAR_MAIN, TokynWarRoom_Text_WarDefeated)
            } else {
                trainerbattle_two_trainers(TRAINER_WAH_ADMIN_TOKYN_ALTERNATIVE, TokynWarRoom_Text_TokynDefeated, TRAINER_WAH_ADMIN_WAR_ALTERNATIVE, TokynWarRoom_Text_WarDefeated)
            }
        }
    }
    special(HealPlayerParty)
    return
}

text TokynWarRoom_Text_TokynDefeated {
    format("Qué bien se sintió este combate… hacía tiempo que no disfrutaba uno así.")
}

text TokynWarRoom_Text_WarDefeated {
    format("Bueno… ahora sí sé quién eres. Vaya forma de presentarte.")
}