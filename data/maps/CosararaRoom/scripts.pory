mapscripts CosararaRoom_MapScripts {
     MAP_SCRIPT_ON_WARP_INTO_MAP_TABLE [
        VAR_TEMP_8, 0: CosararaRoom_OnWarp_PlayerTurnNorth
    ]
    MAP_SCRIPT_ON_LOAD: CosararaRoom_OnLoad
}


script CosararaRoom_OnLoad {
    if(flag(FLAG_DEFEATED_ADMIN_COSARARA)){
        call(WahRoomsShared_SetDoorAsOpen)
    }
    end
}

script CosararaRoom_OnWarp_PlayerTurnNorth {
    turnobject(LOCALID_PLAYER, DIR_NORTH)
    end
}

script CosararaRoom_EventScript_Cosarara {
    lock
    if(flag(FLAG_DEFEATED_ADMIN_COSARARA)) {
        playse(SE_PIN)
        applymovementandwait(LOCALID_COSARARAROOM_COSARARA, moves(face_player, emote_exclamation_mark, delay_16, face_up, delay_16 * 2))
        call(CosararaRoom_EventScript_LockPC)
        delay(20)
        faceplayer
        msgbox(format("¿Y tú eres? Ahhh sí ya me acuerdo de ti, {PLAYER}.\p¡Deseo que te vaya bien!"), MSGBOX_AUTOCLOSE, speaker = SP_NAME_COSARARA)
        applymovementandwait(LOCALID_COSARARAROOM_COSARARA, Common_Movement_FaceUp)
        delay(20)
        call(CosararaRoom_EventScript_UnlockPC)
        release
        end
    }
    msgbox(format("Lo he logrado."), MSGBOX_AUTOCLOSE, speaker = SP_NAME_COSARARA)
    playse(SE_PIN)
    applymovementandwait(LOCALID_COSARARAROOM_COSARARA, Common_Movement_HappyWithDelay48)
    delay(20)
    msgbox(format("He completado una nueva extracción no autorizada de todos los registros de la comunidad.\pLa construimos entre todos, así que creo que tiene un valor que vale la pena preservar."), MSGBOX_AUTOCLOSE, speaker = SP_NAME_COSARARA)
    delay(20)
    faceplayer
    playse(SE_PIN)
    applymovementandwait(LOCALID_COSARARAROOM_COSARARA, moves(face_player, emote_question_mark, delay_16, face_up, delay_16 * 2))
    msgbox(format("Voy a bloquear mi computadora…"), MSGBOX_AUTOCLOSE, speaker = SP_NAME_COSARARA)
    call(CosararaRoom_EventScript_LockPC)
    delay(20)
    faceplayer
    msgbox(format("¿Y tú quién eras? ¿Has cambiado de nick recientemente?\pEs que me pierdo, ¡no me da la memoria!"), MSGBOX_AUTOCLOSE, speaker = SP_NAME_COSARARA)
    playse(SE_PIN)
    applymovementandwait(LOCALID_COSARARAROOM_COSARARA, Common_Movement_SweatDropWithDelay48)
    delay(20)
    specialvar(VAR_RESULT, GetPlayerFacingDirection)
    switch(var(VAR_RESULT)) {
        case DIR_NORTH:
            applymovementandwait(LOCALID_PLAYER, Common_Movement_WalkInPlaceUp)
            break
        case DIR_WEST:
            applymovementandwait(LOCALID_PLAYER,Common_Movement_WalkInPlaceLeft)
            break
        case DIR_EAST:
            applymovementandwait(LOCALID_PLAYER, Common_Movement_WalkInPlaceRight)
            break
    }
    msgbox(format("¿Así que eres {PLAYER} y quieres combatir? Acepto el reto."), MSGBOX_AUTOCLOSE, speaker = SP_NAME_COSARARA)
    call(CosararaRoom_EventScript_CosararaBattle)
    setflag(FLAG_DEFEATED_ADMIN_COSARARA)
    delay(20)
    msgbox(format("Ha sido una buena batalla, {PLAYER}. Creo que te recordaré.\pRecuerda no cambiar de nick, ya que me cuesta mucho saber quién es quien.\pYa puedes avanzar."), MSGBOX_AUTOCLOSE, speaker = SP_NAME_COSARARA)
    call(WahRoomsShared_OpenDoor)
    delay(20)
    applymovementandwait(LOCALID_COSARARAROOM_COSARARA, Common_Movement_FaceUp)
    delay(20)
    call(CosararaRoom_EventScript_UnlockPC)
    release
    end
}

script CosararaRoom_EventScript_CosararaBattle {
    if (!flag(FLAG_WAH_CHALLENGE_COMPLETED)) {
        trainerbattle_no_intro(TRAINER_WAH_ADMIN_COSARARA_MAIN, CosararaRoom_Text_CosararaDefeated)
    } else {
        setvar(VAR_0x8004, TEAM_INDEX_COSARARA)
        special(ShouldUseAlternativeTeam)
        if (var(VAR_RESULT) == 0) {
            trainerbattle_no_intro(TRAINER_WAH_ADMIN_COSARARA_MAIN, CosararaRoom_Text_CosararaDefeated)
        } else {
            trainerbattle_no_intro(TRAINER_WAH_ADMIN_COSARARA_ALTERNATIVE, CosararaRoom_Text_CosararaDefeated)
        }
    }
    special(HealPlayerParty)
    return
}

text CosararaRoom_Text_CosararaDefeated {
    format("Me ha hecho muy feliz esta batalla.")
}

script CosararaRoom_EventScript_LockPC {
    setmetatile(1, 1, METATILE_room_cosarara_PC_LOCKED_1, TRUE)
    setmetatile(2, 1, METATILE_room_cosarara_PC_LOCKED_2, TRUE)
    setmetatile(3, 1, METATILE_room_cosarara_PC_LOCKED_3, TRUE)
    setmetatile(4, 1, METATILE_room_cosarara_PC_LOCKED_4, TRUE)
    setmetatile(5, 1, METATILE_room_cosarara_PC_LOCKED_5, TRUE)
    setmetatile(1, 2, METATILE_room_cosarara_PC_LOCKED_6, TRUE)
    setmetatile(2, 2, METATILE_room_cosarara_PC_LOCKED_7, TRUE)
    setmetatile(3, 2, METATILE_room_cosarara_PC_LOCKED_8, TRUE)
    setmetatile(4, 2, METATILE_room_cosarara_PC_LOCKED_9, TRUE)
    setmetatile(5, 2, METATILE_room_cosarara_PC_LOCKED_10, TRUE)
    special(DrawWholeMapView)
    return
}

script CosararaRoom_EventScript_UnlockPC {
    setmetatile(1, 1, METATILE_room_cosarara_PC_MATRIX_1, TRUE)
    setmetatile(2, 1, METATILE_room_cosarara_PC_MATRIX_2, TRUE)
    setmetatile(3, 1, METATILE_room_cosarara_PC_MATRIX_3, TRUE)
    setmetatile(4, 1, METATILE_room_cosarara_PC_MATRIX_4, TRUE)
    setmetatile(5, 1, METATILE_room_cosarara_PC_MATRIX_5, TRUE)
    setmetatile(1, 2, METATILE_room_cosarara_PC_MATRIX_6, TRUE)
    setmetatile(2, 2, METATILE_room_cosarara_PC_MATRIX_7, TRUE)
    setmetatile(3, 2, METATILE_room_cosarara_PC_MATRIX_8, TRUE)
    setmetatile(4, 2, METATILE_room_cosarara_PC_MATRIX_9, TRUE)
    setmetatile(5, 2, METATILE_room_cosarara_PC_MATRIX_10, TRUE)
    special(DrawWholeMapView)
    return
}