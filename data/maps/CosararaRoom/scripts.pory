mapscripts CosararaRoom_MapScripts {}


script CosararaRoom_EventScript_Cosarara {
    lock
    if(flag(FLAG_DEFEATED_ADMIN_COSARARA)) {
        faceplayer
        msgbox(format("¿Y tú eres? Ahhh sí ya me acuerdo de ti, {PLAYER}.\p¡Deseo que te vaya bien!"))
        applymovementandwait(LOCALID_COSARARAROOM_COSARARA, Common_Movement_FaceUp)
        release
        end
    }
    msgbox(format("Lo he logrado."), MSGBOX_AUTOCLOSE)
    playse(SE_PIN)
    applymovementandwait(LOCALID_COSARARAROOM_COSARARA, Common_Movement_HappyWithDelay48)
    delay(20)
    msgbox(format("He completado una nueva extracción no autorizada de todos los registros de la comunidad.\pLa construimos entre todos, así que creo que tiene un valor que vale la pena preservar."), MSGBOX_AUTOCLOSE)
    delay(20)
    faceplayer
    playse(SE_PIN)
    applymovementandwait(LOCALID_COSARARAROOM_COSARARA, Common_Movement_QuestionMarkWithDelay48)
    msgbox(format("¿Y tú quién eras? ¿Has cambiado de nick recientemente?\pEs que me pierdo, ¡no me da la memoria!"), MSGBOX_AUTOCLOSE)
    playse(SE_PIN)
    applymovementandwait(LOCALID_COSARARAROOM_COSARARA, Common_Movement_SweatDropWithDelay48)
    delay(20)
    specialvar(VAR_RESULT, GetPlayerFacingDirection)
    switch(var(VAR_RESULT)) {
        case DIR_NORTH:
            applymovementandwait(LOCALID_PLAYER, Common_Movement_WalkInPlaceUp)
            break
        case DIR_WEST:
            applymovementandwait(LOCALID_PLAYER,Common_Movement_WalkInPlaceLeft)
            break
        case DIR_EAST:
            applymovementandwait(LOCALID_PLAYER, Common_Movement_WalkInPlaceRight)
            break
    }
    msgbox(format("¿Así que eres {PLAYER} y quieres combatir? Acepto el reto."))
    // Cosarara is admin index 13
    if (!flag(FLAG_WAH_CHALLENGE_COMPLETED)) {
        trainerbattle_no_intro(TRAINER_WAH_ADMIN_COSARARA_MAIN, format("Me ha hecho muy feliz esta batalla."))
    } else {
        setvar(VAR_0x8004, 13)
        special(ShouldUseAlternativeTeam)
        if (var(VAR_RESULT) == 0) {
            trainerbattle_no_intro(TRAINER_WAH_ADMIN_COSARARA_MAIN, format("Me ha hecho muy feliz esta batalla."))
        } else {
            trainerbattle_no_intro(TRAINER_WAH_ADMIN_COSARARA_ALTERNATIVE, format("Me ha hecho muy feliz esta batalla."))
        }
    }
    setflag(FLAG_DEFEATED_ADMIN_COSARARA)
    delay(20)
    msgbox(format("Ha sido una buena batalla, {PLAYER}. Creo que te recordaré.\pRecuerda no cambiar de nick, ya que me cuesta mucho saber quién es quien.\pYa puedes avanzar."), MSGBOX_AUTOCLOSE)
    call(PokemonLeague_EliteFour_OpenRoomDoorMetatiles)
    delay(20)
    applymovementandwait(LOCALID_COSARARAROOM_COSARARA, Common_Movement_FaceUp)
    release
    end
}

