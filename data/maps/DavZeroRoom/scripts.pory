const VAR_TEMP_0_DAVZERO_ROOM_STATE = VAR_TEMP_0
const FLAG_TEMP_1_DAVZERO_ROOM_HIDE_MILOTIC = FLAG_TEMP_1
const FLAG_TEMP_2_DAVZERO_ROOM_HIDE_BALL = FLAG_TEMP_2

mapscripts DavZeroRoom_MapScripts {
    MAP_SCRIPT_ON_FRAME_TABLE [
        VAR_TEMP_0_DAVZERO_ROOM_STATE, 0: DavZeroRoom_OnFrame
	]
    MAP_SCRIPT_ON_TRANSITION: DavZeroRoom_OnTransition
    MAP_SCRIPT_ON_LOAD: DavZeroRoom_OnLoad
    MAP_SCRIPT_ON_WARP_INTO_MAP_TABLE [
        VAR_TEMP_8, 0: DavZeroRoom_OnWarp_PlayerTurnNorth
    ]
}

script DavZeroRoom_OnLoad {
    if(flag(FLAG_DEFEATED_ADMIN_DAVZERO)){
        call(WahRoomsShared_SetDoorAsOpen)
    }
    end
}

script DavZeroRoom_OnWarp_PlayerTurnNorth {
    turnobject(LOCALID_PLAYER, DIR_NORTH)
    end
}

script DavZeroRoom_OnTransition {
    if(var(VAR_TEMP_0_DAVZERO_ROOM_STATE) == 0) {
        setflag(FLAG_TEMP_1_DAVZERO_ROOM_HIDE_MILOTIC)
        setflag(FLAG_TEMP_2_DAVZERO_ROOM_HIDE_BALL)
    }
    end
}

script DavZeroRoom_OnFrame {
    applymovementandwait(LOCALID_PLAYER, moves(walk_up * 8, face_right))
    special(SpawnCameraObject)
    directcameramovementtopos(10,5)
    waitmovement(0)
    msgbox(format("Bien, recordad: las palabras agudas se acentúan cuando acaban en vocal, {COLOR RED}n{COLOR DARK_GRAY} o {COLOR BLUE}s{COLOR DARK_GRAY}."), MSGBOX_AUTOCLOSE, speaker = SP_NAME_DAVZERO)
    playse(SE_PIN)
    applymovement(LOCALID_ZEROROOM_SCHOOLGIRL, DavZeroRoom_Movement_GirlAndBoyLearning)
    applymovement(LOCALID_ZEROROOM_SCHOOLBOY, DavZeroRoom_Movement_GirlAndBoyLearning)
    waitmovement(0)
    waitse
    delay(20)
    turnobject(LOCALID_ZEROROOM_DAVZERO, DIR_WEST)
    playse(SE_PIN)
    applymovementandwait(LOCALID_ZEROROOM_DAVZERO, Common_Movement_DoubleExclamationMarkWithDelay48)
    delay(20)
    applymovementandwait(LOCALID_ZEROROOM_DAVZERO, Common_Movement_WalkInPlaceDown)
    msgbox(format("Esperad un segundo… ¡Creo que hoy tenemos compañía!"), MSGBOX_AUTOCLOSE, speaker = SP_NAME_DAVZERO)
    applymovement(LOCALID_ZEROROOM_DAVZERO, moves(walk_left * 3, walk_up, walk_left, face_down))
    applymovement(LOCALID_CAMERA, Common_Movement_WalkLeft4)
    waitmovement(LOCALID_ZEROROOM_DAVZERO)
    special(RemoveCameraObject)
    turnobject(LOCALID_PLAYER, DIR_NORTH)
    msgbox(format("Ajá, con que por fin has llegado hasta aquí.\pEs un placer verte, ¿me reconoces?"), MSGBOX_AUTOCLOSE, speaker = SP_NAME_DAVZERO)
    dynmultichoice(0,0, TRUE, 3, 0, DYN_MULTICHOICE_CB_NONE, format("Sí, ¡eres inconfundible!"), format("Me suenas, ¡pareces Machoke!"), format("¿Debería conocerte?"))
    if(var(VAR_RESULT) == 2)
    {
        delay(20)
        msgbox(format("En breves sabrás quién soy."), MSGBOX_AUTOCLOSE, speaker = SP_NAME_DAVZERO)
    }
    playse(SE_PIN)
    applymovementandwait(LOCALID_ZEROROOM_DAVZERO, Common_Movement_HappyWithDelay48)
    delay(10)
    applymovementandwait(LOCALID_ZEROROOM_DAVZERO, moves(walk_left * 2, walk_down * 5, face_left))
    playse(SE_PIN)
    applymovementandwait(LOCALID_PLAYER, Common_Movement_QuestionMarkWithDelay48)
    applymovementandwait(LOCALID_PLAYER, moves(walk_down * 4, face_left))
    turnobject(LOCALID_ZEROROOM_DAVZERO, DIR_EAST)
    msgbox(format("He tenido que dejar de hacer mi cuarta serie con 110 kg en press de banca.\pTodo porque escuchaba que un contendiente se acercaba a este nivel.\pY justo me has pillado enseñando las reglas de acentuación a mi alumnado…"), MSGBOX_AUTOCLOSE, speaker = SP_NAME_DAVZERO)
    playse(SE_PIN)
    applymovementandwait(LOCALID_ZEROROOM_DAVZERO, Common_Movement_SweatDropWithDelay48)
    msgbox(format("A ver, menos mal que pueden practicar un rato mientras combatimos.\pAunque creo que estarán muy expectantes, porque presiento que este será un combate sin parangón."), MSGBOX_AUTOCLOSE, speaker = SP_NAME_DAVZERO)
    applymovementandwait(LOCALID_ZEROROOM_DAVZERO, moves(emote_happy, jump_in_place_right * 2, delay_16))
    delay(20)
    applymovementandwait(LOCALID_ZEROROOM_DAVZERO, Common_Movement_WalkRight)
    msgbox(format("Dime… ¿cuál es tu objetivo? ¿quieres desafiar al loco con lanzallamas que te espera al final?"), MSGBOX_AUTOCLOSE, speaker = SP_NAME_DAVZERO)
    applymovementandwait(LOCALID_ZEROROOM_DAVZERO, Common_Movement_WalkLeft2)
    msgbox(format("A estas alturas ya lo sabrás, ya que para llegar aquí te has debido enfrentar a diversos compañeros."), MSGBOX_AUTOCLOSE, speaker = SP_NAME_DAVZERO)
    applymovementandwait(LOCALID_ZEROROOM_DAVZERO, Common_Movement_JumpInPlaceLeftRight)
    msgbox(format("¡Pero yo no te lo pondré nada fácil en este combate!\pHe estado entrenando cuerpo, mente y Pokémon esperando tu llegada."), MSGBOX_AUTOCLOSE, speaker = SP_NAME_DAVZERO)
    applymovementandwait(LOCALID_ZEROROOM_DAVZERO, Common_Movement_WalkRight2)
    msgbox(format("Pocos aspirantes logran llegar hasta aquí. Enhorabuena."), MSGBOX_AUTOCLOSE, speaker = SP_NAME_DAVZERO)
    playse(SE_PIN)
    applymovementandwait(LOCALID_ZEROROOM_DAVZERO, Common_Movement_SmileWithDelay48)
    msgbox(format("Ven, posicionémonos en la zona de combate."), MSGBOX_AUTOCLOSE, speaker = SP_NAME_DAVZERO)
    special(SpawnCameraObject)
    applymovement(LOCALID_ZEROROOM_DAVZERO, moves(walk_up * 3, walk_right, face_down))
    applymovement(LOCALID_PLAYER, Common_Movement_FaceUp)
    applymovement(LOCALID_CAMERA, Common_Movement_WalkUp)
    waitmovement(LOCALID_ZEROROOM_DAVZERO)
    special(RemoveCameraObject)
    msgbox(format("¿Sabes? Muchos creen que un combate es pura fuerza, pero para mí es memoria.\pCada ataque, cada estrategia, cada fallo… es un recordatorio de quiénes fuimos y en qué nos convertimos."), MSGBOX_AUTOCLOSE, speaker = SP_NAME_DAVZERO)
    applymovementandwait(LOCALID_ZEROROOM_DAVZERO, Common_Movement_WalkInPlaceDown)
    msgbox(format("Quiero que te quede claro lo siguiente: no soy tu rival.\pSoy la voz que te recuerda que cada paso, incluso dentro de estas cuatro paredes, puede marcarte más de lo que crees."), MSGBOX_AUTOCLOSE, speaker = SP_NAME_DAVZERO)
    applymovementandwait(LOCALID_ZEROROOM_DAVZERO, Common_Movement_WalkInPlaceDown)
    msgbox(format("Hay quien lucha para ganar, y hay quien lucha para no volver a cometer el mismo fallo.\p¿Cuál de esos eres tú? Déjame comprobarlo, ya que tu mirada me produce curiosidad…\pMe hace recordar aquella época loca en la que frecuentaba aquel lugar que antaño fue nuestro hogar."))
    playse(SE_PIN)
    applymovementandwait(LOCALID_ZEROROOM_DAVZERO, Common_Movement_CryWithDelay48)
    msgbox(format("Bueno, ya está bien, que al final me pongo intenso y esto acabará pareciendo un poema de Benedetti…\pY en esta ocasión no traigo rosas para acompañarlo, sino Pokéball."), MSGBOX_AUTOCLOSE, speaker = SP_NAME_DAVZERO)
    clearflag(FLAG_TEMP_2_DAVZERO_ROOM_HIDE_BALL)
    addobject(LOCALID_ZEROROOM_POKEBALL)
    showobjectat(LOCALID_ZEROROOM_POKEBALL, MAP_DAV_ZERO_ROOM)
    applymovement(LOCALID_ZEROROOM_POKEBALL, Common_Movement_WalkDiagSouthEast)
    waitmovement(0)
	playse(SE_BALL_OPEN)
	fadescreen(FADE_TO_WHITE)
    removeobject(LOCALID_ZEROROOM_POKEBALL)
    clearflag(FLAG_TEMP_1_DAVZERO_ROOM_HIDE_MILOTIC)
    addobject(LOCALID_ZEROROOM_MILOTIC)
    fadescreen(FADE_FROM_WHITE)
    setobjectmovementtype2(LOCALID_ZEROROOM_DAVZERO, MOVEMENT_TYPE_FACE_DOWN)   
    release
    msgbox(format("¡No te amedrentes ahora!"), MSGBOX_AUTOCLOSE, speaker = SP_NAME_DAVZERO)
    call(DavZeroRoom_EventScript_DavZeroBattle)
    fadescreen(FADE_TO_WHITE)
    removeobject(LOCALID_ZEROROOM_MILOTIC)
    fadescreen(FADE_FROM_WHITE)
    applymovementandwait(LOCALID_ZEROROOM_DAVZERO, Common_Movement_WalkDown2)
    msgbox(format("Hoy has sido tú quien ha salido victorioso, y lo reconozco sin rodeos.\pAunque debes tener claro que no siempre te alzarás con la victoria."), MSGBOX_AUTOCLOSE, speaker = SP_NAME_DAVZERO)
    playse(SE_PIN)
    applymovementandwait(LOCALID_ZEROROOM_DAVZERO, Common_Movement_ThinkingWithDelay48)
    msgbox(format("Pero escucha algo: la derrota no es el final de nada. Al contrario, es un espejo.\pRefleja lo que todavía no queremos ver de nosotros mismos, aquello que evitamos afrontar."), MSGBOX_AUTOCLOSE, speaker = SP_NAME_DAVZERO)
    applymovementandwait(LOCALID_ZEROROOM_DAVZERO, Common_Movement_WalkInPlaceDown)
    msgbox(format("Yo también lo he aprendido así, perdiendo, equivocándome, cayendo una y otra vez…"), MSGBOX_AUTOCLOSE, speaker = SP_NAME_DAVZERO)
    playse(SE_PIN)
    applymovement(LOCALID_ZEROROOM_DAVZERO, Common_Movement_HappyWithDelay48)
    msgbox(format("Riéndome."), MSGBOX_AUTOCLOSE, speaker = SP_NAME_DAVZERO)
    waitmovement(0)
    closemessage
    delay(20)
    applymovementandwait(LOCALID_ZEROROOM_DAVZERO, moves(walk_left, emote_thinking, walk_left, walk_right * 4, walk_left * 2, face_down))
    msgbox(format("¿Sabes? El éxito y la victoria no está en acumular triunfos, ni en aplastar rivales, ni siquiera en vencerme a mí ahora.\pEl éxito está en levantarte después de caer, en seguir caminando con las cicatrices bien visibles, en no rendirte aunque la vida o un combate te hayan dejado tocado."), MSGBOX_AUTOCLOSE, speaker = SP_NAME_DAVZERO)
    delay(10)
    msgbox(format("Quiero que entiendas eso."), MSGBOX_AUTOCLOSE, speaker = SP_NAME_DAVZERO)
    applymovementandwait(LOCALID_ZEROROOM_DAVZERO, Common_Movement_WalkInPlaceDown)
    delay(20)
    msgbox(format("Hoy me has derrotado, y me alegra que lo hayas hecho.\pPorque eso significa que tú también estás aprendiendo lo que de verdad importa…\pY que yo también tengo cosas que aprender."), MSGBOX_AUTOCLOSE, speaker = SP_NAME_DAVZERO    )
    playse(SE_PIN)
    applymovementandwait(LOCALID_ZEROROOM_DAVZERO, Common_Movement_HeartWithDelay48)
    msgbox(format("Quédate con eso, porque es un conocimiento que ningún Pokémon, ningún rival y ninguna medalla pueden quitarte jamás."), MSGBOX_AUTOCLOSE, speaker = SP_NAME_DAVZERO)
    applymovementandwait(LOCALID_ZEROROOM_DAVZERO, Common_Movement_WalkInPlaceDown)
    msgbox(format("Definitivamente no me equivocaba: tienes una mirada diferente.\pEstoy orgulloso de que hayas vencido, porque cada victoria como la tuya mantiene viva la llama de este lugar."), MSGBOX_AUTOCLOSE, speaker = SP_NAME_DAVZERO)
    playse(SE_PIN)
    applymovementandwait(LOCALID_ZEROROOM_DAVZERO, Common_Movement_HappyWithDelay48)
    delay(10)
    applymovementandwait(LOCALID_ZEROROOM_DAVZERO, Common_Movement_WalkUp2)
    msgbox(format("Aquí termina nuestro encuentro, pero no tu camino.\pYo solo fui un obstáculo, como esas mancuernas que parecen imposibles hasta que un día te das cuenta de que puedes con ellas."), MSGBOX_AUTOCLOSE, speaker = SP_NAME_DAVZERO)
    delay(20)
    applymovementandwait(LOCALID_ZEROROOM_DAVZERO, Common_Movement_FaceDown)
    msgbox(format("Y oye, si algún día coincidimos en el gimnasio, no me mires raro si te pido que me ayudes a subir la barra.\pNadie es invencible."), MSGBOX_AUTOCLOSE, speaker = SP_NAME_DAVZERO)
    delay(20)
    applymovementandwait(LOCALID_ZEROROOM_DAVZERO, Common_Movement_WalkDown2)
    msgbox(format("Ahora ve, compañero. Sigue tu camino, comete errores y aprende."), MSGBOX_AUTOCLOSE, speaker = SP_NAME_DAVZERO)
    applymovementandwait(LOCALID_ZEROROOM_DAVZERO, Common_Movement_WalkInPlaceDown)
    msgbox(format("No tengas miedo de perder. Lo peor que puedes hacer es dejar de intentarlo por miedo a no estar a la altura.\pYo lo sé bien… demasiadas veces me quedé paralizado pensando en qué dirán de mí, en si mis errores me definían."), MSGBOX_AUTOCLOSE, speaker = SP_NAME_DAVZERO)
    applymovementandwait(LOCALID_ZEROROOM_DAVZERO, Common_Movement_ThinkingWithDelay48)
    msgbox(format("No eres tus fracasos, ni tus caídas, ni lo que el resto piense de ti.\pEres todo lo que haces a pesar de ellos. Si hoy has sido capaz de vencerme aquí, en este lugar, recuerda que también serás capaz de enfrentarte a cualquier cosa ahí fuera."), MSGBOX_AUTOCLOSE, speaker = SP_NAME_DAVZERO)
    applymovementandwait(LOCALID_ZEROROOM_DAVZERO, Common_Movement_WalkInPlaceDown)
    msgbox(format("¿Qué pasa, por qué me miras así?"), MSGBOX_AUTOCLOSE, speaker = SP_NAME_DAVZERO)
    dynmultichoice(0,0, TRUE, 4, 0, DYN_MULTICHOICE_CB_NONE, format("Gran consejo, digno de un sabio."), format("Me hiciste pensar un poco."), format("¿Falta mucho del discurso?"), format("¿Eso fue una clase?"))
    msgbox(format("¿No te esperabas esto? Qué quieres que te diga… Yo tan solo soy Zero."), MSGBOX_AUTOCLOSE, speaker = SP_NAME_DAVZERO   )
    playse(SE_PIN)
    applymovementandwait(LOCALID_ZEROROOM_DAVZERO, Common_Movement_SmileWithDelay48)
    special(SpawnCameraObject)
    directcameramovementtopos(6, 5)
    waitmovement(0)
	call(WahRoomsShared_OpenDoor)
    directcameramovementtopos(6, 9)
    waitmovement(0)
    special(RemoveCameraObject)
    applymovementandwait(LOCALID_ZEROROOM_DAVZERO, moves(walk_up * 3, walk_right * 4, face_down))
    setvar(VAR_TEMP_0_DAVZERO_ROOM_STATE, 1)
    setflag(FLAG_DEFEATED_ADMIN_DAVZERO)
    release
    end
}

script DavZeroRoom_EventScript_DavZeroBattle {
    if (!flag(FLAG_WAH_CHALLENGE_COMPLETED)) {
        trainerbattle_no_intro(TRAINER_WAH_ADMIN_DAVZERO_MAIN, DavZeroRoom_Text_DavZeroDefeated)
    } else {
        setvar(VAR_0x8004, TEAM_INDEX_DAVZERO)
        special(ShouldUseAlternativeTeam)
        if (var(VAR_RESULT) == 0) {
            trainerbattle_no_intro(TRAINER_WAH_ADMIN_DAVZERO_MAIN, DavZeroRoom_Text_DavZeroDefeated)
        } else {
            trainerbattle_no_intro(TRAINER_WAH_ADMIN_DAVZERO_ALTERNATIVE, DavZeroRoom_Text_DavZeroDefeated)
        }
    }
    special(HealPlayerParty)
    return
}

text DavZeroRoom_Text_DavZeroDefeated {
    format("Uf… no puedo negarlo, me has sorprendido.\pHa sido uno de esos combates que a\nmí me gustan: intensos, donde cada movimiento parece pesar el doble\ly cada decisión importa.")
}
movement DavZeroRoom_Movement_GirlAndBoyLearning {
    emote_idea
    walk_in_place_up
    delay_16 * 2
}

script DavZeroRoom_EventScript_SchoolGirl {
    specialvar(VAR_RESULT, GetPlayerFacingDirection)
    if(var(VAR_RESULT) == DIR_NORTH) {
        end
    }
    lock
    faceplayer
    msgbox(format("¿Te cuento un secreto? Ya terminé la escuela hace años.\p¡Solamente vengo aquí porque Zero es tan interesante!\pPodría quedarme todo el día escuchándolo hablar."), MSGBOX_AUTOCLOSE)
    turnobject(LOCALID_ZEROROOM_SCHOOLGIRL, DIR_NORTH)
    release
    end
}

script DavZeroRoom_EventScript_SchoolBoy {
    specialvar(VAR_RESULT, GetPlayerFacingDirection)
    if(var(VAR_RESULT) == DIR_NORTH) {
        end
    }
    lock
    faceplayer
    msgbox(format("¡El profe Zero es muy guay!"))
    playse(SE_PIN)
    applymovementandwait(LOCALID_ZEROROOM_SCHOOLBOY, Common_Movement_SmileWithDelay48)
    msgbox(format("Cuando teníamos seis años nos enseñó inglés y aprendimos los meses del año al ritmo de La Macarena."), MSGBOX_AUTOCLOSE)
    playse(SE_PIN)
    applymovementandwait(LOCALID_PLAYER, Common_Movement_SurpriseWithDelay48)
    delay(20)
    turnobject(LOCALID_ZEROROOM_SCHOOLBOY, DIR_NORTH)
    release
    end
}

script DavZeroRoom_EventScript_Zero {
    msgbox(format("Pendiente"), MSGBOX_NPC)
    end
}