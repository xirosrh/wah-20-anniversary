mapscripts KleinPkPowerRoom_MapScripts {
    MAP_SCRIPT_ON_FRAME_TABLE [
		VAR_TEMP_0, 0: KleinPkPowerRoom_OnFrame
	]
    MAP_SCRIPT_ON_LOAD: KleinPkPowerRoom_OnLoad
    MAP_SCRIPT_ON_WARP_INTO_MAP_TABLE [
        VAR_TEMP_8, 0: KleinPkPowerRoom_OnWarp_PlayerTurnNorth
    ]
}


script KleinPkPowerRoom_OnLoad {
    if(flag(FLAG_DEFEATED_ADMIN_KLEIN) && flag(FLAG_DEFEATED_ADMIN_PKPOWER)){
        setmetatile(7, 5, METATILE_room_kleinpkpower_SCREEN_JAIL_OPEN, 1)
        setmetatile(9, 5, METATILE_room_kleinpkpower_JAIL_FLOOR, 0)
        setmetatileelevation(9, 6, 0)
        setobjectxyperm(LOCALID_KLEINROOM_PKPOWER, 2, 4)
        setobjectmovementtype(LOCALID_KLEINROOM_PKPOWER, MOVEMENT_TYPE_FACE_UP)
        setobjectxyperm(LOCALID_KLEINROOM_KLEIN, 2, 7)
        setobjectmovementtype(LOCALID_KLEINROOM_KLEIN, MOVEMENT_TYPE_FACE_UP)
        call(WahRoomsShared_SetDoorAsOpen)
    }
    end
}

script KleinPkPowerRoom_OnWarp_PlayerTurnNorth {
    turnobject(LOCALID_PLAYER, DIR_NORTH)
    end
}

script KleinPkPowerRoom_OnFrame {
    lock
    applymovement(LOCALID_PLAYER, moves(walk_up * 7))
    waitmovement(LOCALID_PLAYER)
    // Primera llamada
    playse(SE_POKENAV_CALL)
    waitse
    playse(SE_POKENAV_CALL)
    waitse
    playse(SE_PIN)
    applymovement(LOCALID_KLEINROOM_PKPOWER, moves(emote_exclamation_mark))
    waitmovement(LOCALID_KLEINROOM_PKPOWER)
    waitse
    playse(SE_POKENAV_CALL)
    waitse
    playse(SE_POKENAV_CALL)
    waitse
    delay(30)
    playse(SE_POKENAV_ON)
    waitse
    applymovement(LOCALID_KLEINROOM_PKPOWER, moves(walk_in_place_up))
    waitmovement(LOCALID_KLEINROOM_PKPOWER)
    msgbox(format("Aquí el soporte técnico.\n¿En qué puedo ayudarte?"), MSGBOX_AUTOCLOSE, speaker = SP_NAME_PKPOWER)
    applymovement(LOCALID_KLEINROOM_PKPOWER, moves(emote_thinking))
    delay(60)
    waitmovement(LOCALID_KLEINROOM_PKPOWER)
    msgbox(format("¿Que el Güindous no arranca?\n¿Y qué aparece en pantalla?"), MSGBOX_AUTOCLOSE, speaker = SP_NAME_PKPOWER)
    applymovement(LOCALID_KLEINROOM_PKPOWER, moves(emote_thinking))
    delay(60)
    waitmovement(LOCALID_KLEINROOM_PKPOWER)
    msgbox(format("¿Nada?\n¿Pero… tienes el ordenador encendido?"), MSGBOX_AUTOCLOSE, speaker = SP_NAME_PKPOWER)
    applymovement(LOCALID_KLEINROOM_PKPOWER, moves(emote_thinking))
    delay(60)
    waitmovement(LOCALID_KLEINROOM_PKPOWER)
    playse(SE_PIN)
    applymovement(LOCALID_KLEINROOM_PKPOWER, moves(emote_angry))
    msgbox(format("¡Pues enchúfalo, hombre!\n¡Esas cosas se comprueban antes!"), MSGBOX_AUTOCLOSE, speaker = SP_NAME_PKPOWER)
    waitse
    waitmovement(LOCALID_KLEINROOM_PKPOWER)
    playse(SE_POKENAV_HANG_UP)
    waitse
    // Segunda llamada
    delay(60)
    playse(SE_POKENAV_CALL)
    waitse
    playse(SE_POKENAV_CALL)
    waitse
    delay(30)
    playse(SE_POKENAV_ON)
    waitse
    applymovement(LOCALID_KLEINROOM_PKPOWER, moves(walk_in_place_up))
    waitmovement(LOCALID_KLEINROOM_PKPOWER)
    msgbox(format("Aquí el soporte técnico.\n¿Qué necesitas?"), MSGBOX_AUTOCLOSE, speaker = SP_NAME_PKPOWER)
    applymovement(LOCALID_KLEINROOM_PKPOWER, moves(emote_thinking))
    delay(60)
    waitmovement(LOCALID_KLEINROOM_PKPOWER)
    msgbox(format("¿¡Cómo que has perdido el cursor!?\n¿Has probado a mover el ratón?"), MSGBOX_AUTOCLOSE, speaker = SP_NAME_PKPOWER)
    applymovement(LOCALID_KLEINROOM_PKPOWER, moves(emote_thinking))
    delay(60)
    waitmovement(LOCALID_KLEINROOM_PKPOWER)
    msgbox(format("No será el ratón inalámbrico...\n¿Tiene pilas?"), MSGBOX_AUTOCLOSE, speaker = SP_NAME_PKPOWER)
    applymovement(LOCALID_KLEINROOM_PKPOWER, moves(emote_thinking))
    delay(60)
    waitmovement(LOCALID_KLEINROOM_PKPOWER)
    playse(SE_PIN)
    applymovement(LOCALID_KLEINROOM_PKPOWER, moves(emote_angry))
    msgbox(format("¡Joder! ¡Pues pónselas!"), MSGBOX_AUTOCLOSE, speaker = SP_NAME_PKPOWER)
    waitse
    waitmovement(LOCALID_KLEINROOM_PKPOWER)
    playse(SE_POKENAV_HANG_UP)
    waitse
    // Tercera llamada
    delay(60)
    playse(SE_POKENAV_CALL)
    waitse
    playse(SE_POKENAV_CALL)
    waitse
    delay(30)
    playse(SE_POKENAV_ON)
    waitse
    applymovement(LOCALID_KLEINROOM_PKPOWER, moves(walk_in_place_up))
    waitmovement(LOCALID_KLEINROOM_PKPOWER)
    msgbox(format("Aquí el soporte técnico."), MSGBOX_AUTOCLOSE, speaker = SP_NAME_PKPOWER)
    applymovement(LOCALID_KLEINROOM_PKPOWER, moves(emote_thinking))
    delay(60)
    waitmovement(LOCALID_KLEINROOM_PKPOWER)
    msgbox(format("¿¡Que cuál es la tecla de “cualquier tecla”!? ¿¡Pero qué pregunta es esa!?"), MSGBOX_AUTOCLOSE, speaker = SP_NAME_PKPOWER)
    applymovement(LOCALID_KLEINROOM_PKPOWER, moves(emote_thinking))
    delay(60)
    waitmovement(LOCALID_KLEINROOM_PKPOWER)
    msgbox(format("¡Pues si el mensaje dice “pulse cualquier tecla”, haz lo que dice!"), MSGBOX_AUTOCLOSE, speaker = SP_NAME_PKPOWER)
    applymovement(LOCALID_KLEINROOM_PKPOWER, moves(emote_thinking))
    delay(60)
    waitmovement(LOCALID_KLEINROOM_PKPOWER)
    playse(SE_PIN)
    applymovement(LOCALID_KLEINROOM_PKPOWER, moves(emote_angry))
    msgbox(format("¡Que no hay ninguna tecla que se llame “cualquier tecla”! ¡Pulsa una tecla cualquiera y ya!"), MSGBOX_AUTOCLOSE, speaker = SP_NAME_PKPOWER)
    waitse
    waitmovement(LOCALID_KLEINROOM_PKPOWER)
    playse(SE_POKENAV_HANG_UP)
    waitse
    playbgm(MUS_ENCOUNTER_MAGMA, FALSE)
    playse(SE_PIN)
    applymovement(LOCALID_KLEINROOM_PKPOWER, moves(emote_angry walk_in_place_up))
    msgbox(format("¡Esto es increíble! ¿Pero cómo puede haber gente así? ¡Estoy rodeado de inútiles! Pero se va a acabar...\p¡Los voy a echar a todos!\n¡A la puta calle!"), MSGBOX_AUTOCLOSE, speaker = SP_NAME_PKPOWER)
    waitse
    waitmovement(LOCALID_KLEINROOM_PKPOWER)
    playse(SE_LEDGE)
    applymovement(LOCALID_KLEINROOM_KLEIN, moves(jump_in_place_down))
    waitse
    waitmovement(LOCALID_KLEINROOM_KLEIN)
    playse(SE_LEDGE)
    applymovement(LOCALID_KLEINROOM_KLEIN, moves(jump_in_place_down))
    waitse
    waitmovement(LOCALID_KLEINROOM_KLEIN)
    msgbox(format("¡Eh, tú! ¡Aquí! ¡Ven!"), MSGBOX_AUTOCLOSE, speaker = SP_NAME_KLEIN)
    playse(SE_PIN)
    applymovement(LOCALID_PLAYER, moves(emote_question_mark face_right))
    waitse
    waitmovement(LOCALID_PLAYER)
    fadedefaultbgm
    delay(60)
    applymovement(LOCALID_PLAYER, moves(walk_down walk_right * 4 walk_up))
    waitmovement(LOCALID_PLAYER)
    playbgm(MUS_B_PIKE, FALSE)
    msgbox(format("¿Sabes quién soy?"), MSGBOX_AUTOCLOSE, speaker = SP_NAME_KLEIN)
    playse(SE_PIN)
    applymovement(LOCALID_PLAYER, moves(emote_thinking))
    waitse
    waitmovement(LOCALID_PLAYER)
    delay(30)
    msgbox(format("Claro... Tú qué vas a saber...\nSoy Klein, el webmaster de WaH.\pOye... Tienes que ayudarme, ¡la he liado parda!\pHe hackeado al Perro Sanxe y para colmo me han metido en esta cárcel a la vez que se ha caído el foro...\n¿Puedes liberarme?"), MSGBOX_AUTOCLOSE, speaker = SP_NAME_KLEIN)
    playse(SE_PIN)
    applymovement(LOCALID_PLAYER, moves(emote_question_mark))
    waitse
    waitmovement(LOCALID_PLAYER)
    delay(30)
    msgbox(format("¿Que no sabes cómo liberarme? ¡Pues es muy fácil!\p¿Ves ese botón rojo?"), MSGBOX_AUTOCLOSE, speaker = SP_NAME_KLEIN)
    applymovement(LOCALID_PLAYER, moves(face_left delay_16*4 face_up))
    waitmovement(LOCALID_PLAYER)
    msgbox(format("¡Sí, ese botón! ¡Púlsalo y podré salir!\p¡Vamos, no me hagas esperar más, que tengo que levantar el foro!"), MSGBOX_AUTOCLOSE, speaker = SP_NAME_KLEIN)
    fadedefaultbgm
    setvar(VAR_TEMP_0, 1)
    release
    end
}

script KleinPkPowerRoom_EventScript_BotonRojo {
    lock
    if(flag(FLAG_DEFEATED_ADMIN_PKPOWER))
    {
        msgbox(format("La celda ya está abierta."), MSGBOX_AUTOCLOSE)
    }
    else
    {
        msgbox(format("¿Quieres pulsar el botón rojo?"), MSGBOX_YESNO)
        closemessage
        if(var(VAR_RESULT) == YES)
        {
            playse(SE_CLICK)
            waitse
            delay(30)
            playse(SE_SUCCESS)
            setmetatile(7, 5, METATILE_room_kleinpkpower_SCREEN_JAIL_OPEN, 1)
            special(DrawWholeMapView)
            waitse
            delay(30)
            playse(SE_UNLOCK)
            call(KleinPkPowerRoom_EventScript_OpenJail)
            applymovement(LOCALID_PLAYER, moves(face_right))
            applymovement(LOCALID_KLEINROOM_KLEIN, moves(walk_left walk_down))
            waitmovement(LOCALID_KLEINROOM_KLEIN)
            playse(SE_LEDGE)
            applymovement(LOCALID_KLEINROOM_KLEIN, moves(jump_in_place_down))
            waitse
            waitmovement(LOCALID_KLEINROOM_KLEIN)
            playse(SE_LEDGE)
            applymovement(LOCALID_KLEINROOM_KLEIN, moves(jump_in_place_down))
            waitse
            waitmovement(LOCALID_KLEINROOM_KLEIN)
            playse(SE_PIN)
            applymovement(LOCALID_KLEINROOM_KLEIN, moves(emote_happy))
            waitse
            waitmovement(LOCALID_KLEINROOM_KLEIN)
            playbgm(MUS_B_PIKE, FALSE)
            msgbox(format("¡Sí! ¡Soy libre!"), MSGBOX_AUTOCLOSE, speaker = SP_NAME_KLEIN)
            applymovement(LOCALID_KLEINROOM_KLEIN, moves(walk_down walk_left))
            waitmovement(LOCALID_KLEINROOM_KLEIN)
            playse(SE_PIN)
            applymovement(LOCALID_KLEINROOM_KLEIN, moves(emote_question_mark))
            waitse
            waitmovement(LOCALID_KLEINROOM_KLEIN)
            msgbox(format("Por cierto... ¿Tú quién eras?\n¿No será una trampa para encerrarme de nuevo?"), MSGBOX_AUTOCLOSE, speaker = SP_NAME_KLEIN)
            playse(SE_PIN)
            applymovement(LOCALID_KLEINROOM_KLEIN, moves(emote_versus))
            waitse
            call(KleinPkPowerRoom_EventScript_KleinBattle)
            playse(SE_PIN)
            applymovement(LOCALID_KLEINROOM_PKPOWER, moves(face_down emote_question_mark))
            waitmovement(LOCALID_KLEINROOM_PKPOWER)
            waitse
            applymovement(LOCALID_KLEINROOM_PKPOWER, moves(walk_right*3 walk_down*3 face_right))
            waitmovement(LOCALID_KLEINROOM_PKPOWER)
            playse(SE_PIN)
            applymovement(LOCALID_KLEINROOM_PKPOWER, moves(emote_double_exclamation_mark))
            waitmovement(LOCALID_KLEINROOM_PKPOWER)
            waitse
            playbgm(MUS_ENCOUNTER_MAGMA, FALSE)
            msgbox(format("¿¡Pero qué!?"), MSGBOX_AUTOCLOSE, speaker = SP_NAME_PKPOWER)
            applymovement(LOCALID_KLEINROOM_PKPOWER, moves(walk_right))
            waitmovement(LOCALID_KLEINROOM_PKPOWER)
            applymovement(LOCALID_PLAYER, moves(face_left))
            waitmovement(LOCALID_PLAYER)
            msgbox(format("¿Qué estáis haciendo?\n¡Si acabo de pagar la fianza para sacarlo de la cárcel!\p¿No podías quedarte quietecito tú también? Bah, qué más da...\p¡Venga, a levantar el foro!\n¡Que lleva caído todo el día!"), MSGBOX_AUTOCLOSE, speaker = SP_NAME_PKPOWER)
            playse(SE_PIN)
            applymovement(LOCALID_KLEINROOM_PKPOWER, moves(emote_versus))
            waitse
            call(KleinPkPowerRoom_EventScript_PkPowerBattle)
            msgbox(format("¡Perfecto! ¡Has superado la prueba!\nHay que saber actuar bajo presión.\p¡Vamos, a levantar el foro!"), MSGBOX_AUTOCLOSE, speaker = SP_NAME_PKPOWER)
            fadescreen(FADE_TO_BLACK)
            setobjectxy(LOCALID_KLEINROOM_PKPOWER, 2, 4)
            setobjectxyperm(LOCALID_KLEINROOM_PKPOWER, 2, 4)
            setobjectmovementtype(LOCALID_KLEINROOM_PKPOWER, MOVEMENT_TYPE_FACE_UP)
            setobjectxy(LOCALID_KLEINROOM_KLEIN, 2, 7)
            setobjectxyperm(LOCALID_KLEINROOM_KLEIN, 2, 7)
            setobjectmovementtype(LOCALID_KLEINROOM_KLEIN, MOVEMENT_TYPE_FACE_UP)
            applymovement(LOCALID_KLEINROOM_PKPOWER, moves(face_up))
            applymovement(LOCALID_KLEINROOM_KLEIN, moves(face_up))
            applymovement(LOCALID_PLAYER, moves(walk_left walk_up))
            waitmovement(LOCALID_NONE)
            fadescreen(FADE_FROM_BLACK)
            call(WahRoomsShared_OpenDoor)
        }
    }
    release
    end
}

script KleinPkPowerRoom_EventScript_OpenJail {
    setmetatile(9, 5, METATILE_room_kleinpkpower_JAIL_DOOR_OPENING_1, 0)
    special(DrawWholeMapView)
    delay(10)
    setmetatile(9, 5, METATILE_room_kleinpkpower_JAIL_DOOR_OPENING_2, 0)
    special(DrawWholeMapView)
    delay(10)
    setmetatile(9, 5, METATILE_room_kleinpkpower_JAIL_DOOR_OPENING_3, 0)
    special(DrawWholeMapView)
    delay(10)
    setmetatile(9, 5, METATILE_room_kleinpkpower_JAIL_FLOOR, 0)
    special(DrawWholeMapView)
    waitse
    setmetatileelevation(9, 6, 0)
    return
}

script KleinPkPowerRoom_EventScript_KleinBattle {
    if (!flag(FLAG_WAH_CHALLENGE_COMPLETED)) {
        trainerbattle_no_intro(TRAINER_WAH_ADMIN_KLEIN_MAIN, KleinPkPowerRoom_Text_KleinDefeated)
    } else {
        setvar(VAR_0x8004, TEAM_INDEX_KLEIN)
        special(ShouldUseAlternativeTeam)
        if (var(VAR_RESULT) == 0) {
            trainerbattle_no_intro(TRAINER_WAH_ADMIN_KLEIN_MAIN, KleinPkPowerRoom_Text_KleinDefeated)
        } else {
            trainerbattle_no_intro(TRAINER_WAH_ADMIN_KLEIN_ALTERNATIVE, KleinPkPowerRoom_Text_KleinDefeated)
        }
    }
    setflag(FLAG_DEFEATED_ADMIN_KLEIN)
    special(HealPlayerParty)
    return
}

text KleinPkPowerRoom_Text_KleinDefeated {
    format("¿Y ahora qué?\n¿Al talego de nuevo?")
}

script KleinPkPowerRoom_EventScript_PkPowerBattle {
    if (!flag(FLAG_WAH_CHALLENGE_COMPLETED)) {
        trainerbattle_no_intro(TRAINER_WAH_ADMIN_PKPOWER_MAIN, KleinPkPowerRoom_Text_PkPowerDefeated)
    } else {
        setvar(VAR_0x8004, TEAM_INDEX_PKPOWER)
        special(ShouldUseAlternativeTeam)
        if (var(VAR_RESULT) == 0) {
            trainerbattle_no_intro(TRAINER_WAH_ADMIN_PKPOWER_MAIN, KleinPkPowerRoom_Text_PkPowerDefeated)
        } else {
            trainerbattle_no_intro(TRAINER_WAH_ADMIN_PKPOWER_ALTERNATIVE, KleinPkPowerRoom_Text_PkPowerDefeated)
        }
    }
    setflag(FLAG_DEFEATED_ADMIN_PKPOWER)
    special(HealPlayerParty)
    return
}

text KleinPkPowerRoom_Text_PkPowerDefeated {
    format("¡Vaya tela! ¡A seguir trabajando!")
}

script KleinPkPowerRoom_EventScript_PkPower {
    lock
    faceplayer
    if(flag(FLAG_DEFEATED_ADMIN_PKPOWER)) {
        msgbox(format("¿Pero mira que eres pesado eh? ¿Tú no tienes amigos u otra persona a la que molestar?"), MSGBOX_AUTOCLOSE, speaker = SP_NAME_PKPOWER)
    }
    else {
        msgbox(format("¿No ves que estoy ocupado?"), MSGBOX_AUTOCLOSE, speaker = SP_NAME_PKPOWER)
    }
    applymovement(LOCALID_KLEINROOM_PKPOWER, moves(face_up))
    waitmovement(LOCALID_KLEINROOM_PKPOWER)
    release
    end
}

script KleinPkPowerRoom_EventScript_Klein {
    lock
    faceplayer
    msgbox(format("¿Equipo de Investigación?\n¿O Equipo de la mierda?\pQué te importa a ti el hackeo al Perro."), MSGBOX_AUTOCLOSE, speaker = SP_NAME_KLEIN)
    applymovement(LOCALID_KLEINROOM_KLEIN, moves(face_up))
    waitmovement(LOCALID_KLEINROOM_KLEIN)
    release
    end
}