const VAR_TEMP_0_AGUIAR_ROOM_STATE = VAR_TEMP_0
const FLAG_TEMP_1_HIDE_AGUIAR = FLAG_TEMP_1
const FLAG_TEMP_2_HIDE_AGUIAR_SMOKING = FLAG_TEMP_2

mapscripts AguiarRoom_MapScripts {
    MAP_SCRIPT_ON_WARP_INTO_MAP_TABLE [
        VAR_TEMP_8, 0: AguiarRoom_OnWarp_PlayerTurnNorth
    ]
    MAP_SCRIPT_ON_LOAD: AguiarRoom_OnLoad
    MAP_SCRIPT_ON_FRAME_TABLE [
        VAR_TEMP_0_AGUIAR_ROOM_STATE, 0: AguiarRoom_OnFrame
    ]
}


script AguiarRoom_OnFrame {
    setflag(FLAG_TEMP_2_HIDE_AGUIAR_SMOKING)
    removeobject(LOCALID_AGUIARROOM_AGUIAR_SMOKING)
    getplayerxy(VAR_TEMP_A, VAR_TEMP_B)
    special(SpawnCameraObject)
    directcameramovementtopos(10, 6)
    waitmovement(0)
    playse(SE_PIN)
    applymovement(LOCALID_AGUIARROOM_AGUIAR, Common_Movement_HappyWithDelay48)
    msgbox(format("¡Pero qué buen programa! Hoy si que es un día tranquilo."), MSGBOX_AUTOCLOSE, speaker = SP_NAME_AGUIAR)
    waitmovement(0)
    delay(20)
    msgbox(format("Bueno, hora de dar un par de caladas."), MSGBOX_AUTOCLOSE, speaker = SP_NAME_AGUIAR)
    call(AguiarRoom_EventScript_TurnOffTV)
    delay(30)
    applymovementandwait(LOCALID_AGUIARROOM_AGUIAR, moves(walk_down, walk_left * 4, walk_up, face_down))
    addobject(LOCALID_AGUIARROOM_AGUIAR_SMOKING)
    clearflag(FLAG_TEMP_2_HIDE_AGUIAR_SMOKING)
    removeobject(LOCALID_AGUIARROOM_AGUIAR)
    setflag(FLAG_TEMP_1_HIDE_AGUIAR)
    directcameramovementtoposwithvars(VAR_TEMP_A, VAR_TEMP_B)
    waitmovement(0)
    special(RemoveCameraObject)
    setvar(VAR_TEMP_0_AGUIAR_ROOM_STATE, 1)
    release
    end
}

script AguiarRoom_OnLoad {
    if(var(VAR_TEMP_0_AGUIAR_ROOM_STATE) == 0){
        call(AguiarRoom_EventScript_TurnOnTV)
    }
    if(flag(FLAG_DEFEATED_ADMIN_AGUIAR)){
        call(WahRoomsShared_SetDoorAsOpen)
    }
    end
}

script AguiarRoom_OnWarp_PlayerTurnNorth {
    turnobject(LOCALID_PLAYER, DIR_NORTH)
    end
}

script AguiarRoom_EventScript_TurnOffTV {
    setmetatile(9, 2, METATILE_room_aguiar_TV_OFF_LEFT, TRUE)
    setmetatile(10, 2, METATILE_room_aguiar_TV_OFF_RIGHT, TRUE)
    special(DrawWholeMapView)
    return
}

script AguiarRoom_EventScript_TurnOnTV {
    setmetatile(9, 2, METATILE_room_aguiar_TV_ON_LEFT, TRUE)
    setmetatile(10, 2, METATILE_room_aguiar_TV_ON_RIGHT, TRUE)
    special(DrawWholeMapView)
    return
}

script AguiarRoom_EventScript_Aguiar {
    if(flag(FLAG_DEFEATED_ADMIN_AGUIAR)){
        msgbox(format("Aquí sigo, como siempre: tranquilo, fumando y esperando a ver quién aparece."), MSGBOX_NPC, speaker = SP_NAME_AGUIAR)
        release
        end
    }
    lock
    setobjectmovementtype2(LOCALID_AGUIARROOM_AGUIAR_SMOKING, MOVEMENT_TYPE_FACE_DOWN)
    faceplayer
    msgbox(format("¡Pero mírate! Al final has llegado hasta aquí.\pY yo que pensaba que iba a pasarme el día tirado, con el cigarro y poco más…"), MSGBOX_AUTOCLOSE, speaker = SP_NAME_AGUIAR)
    applymovementandwait(LOCALID_AGUIARROOM_AGUIAR_SMOKING, Common_Movement_SmokeCigarette)
    delay(20)
    msgbox(format("No esperaba visitas, pero oye, me viene de lujo: así me entretengo un rato."), MSGBOX_AUTOCLOSE, speaker = SP_NAME_AGUIAR)
    playse(SE_PIN)
    applymovementandwait(LOCALID_AGUIARROOM_AGUIAR_SMOKING, Common_Movement_HappyWithDelay48)
    waitse
    msgbox(format("Aquí todos se lo toman tan en serio, como si esto fuese cuestión de vida o muerte.\pYo prefiero reírme, dar un par de caladas, y ver qué trae el que se planta delante."), MSGBOX_AUTOCLOSE, speaker = SP_NAME_AGUIAR)
    applymovementandwait(LOCALID_AGUIARROOM_AGUIAR_SMOKING, Common_Movement_SmokeCigarette)
    delay(20)
    msgbox(format("No te voy a dar discursos épicos ni consejos de abuelo, que para eso hay otros.\p
                Si quieres pelear, perfecto. Si pierdo, pues nada, me enciendo otro.\p
                Y si gano… bueno, tampoco me voy a hacer un póster con tu cara, ¿eh?"), MSGBOX_AUTOCLOSE, speaker = SP_NAME_AGUIAR)
    playse(SE_PIN)
    applymovementandwait(LOCALID_AGUIARROOM_AGUIAR_SMOKING, Common_Movement_HappyWithDelay48)
    waitse
    msgbox(format("Así que venga, no le demos más vueltas. Saca a tus Pokémon, que yo ya tengo ganas de ver si al menos me haces dejar el cigarro quieto un rato.\p¡Vamos allá!"), MSGBOX_AUTOCLOSE, speaker = SP_NAME_AGUIAR)
    call(AguiarRoom_EventScript_AguiarBattle)
    delay(10)
    msgbox(format("¡Pues mira que me la has liado! Y yo pensando que iba a ser otro paseo…"), MSGBOX_AUTOCLOSE, speaker = SP_NAME_AGUIAR)
    applymovementandwait(LOCALID_AGUIARROOM_AGUIAR_SMOKING, Common_Movement_SmokeCigarette)
    delay(20)
    msgbox(format("No está mal, no está nada mal. Ya puedes seguir avanzando."), MSGBOX_AUTOCLOSE, speaker = SP_NAME_AGUIAR)
    call(WahRoomsShared_OpenDoor)
    setflag(FLAG_DEFEATED_ADMIN_AGUIAR)
    setobjectmovementtype2(LOCALID_AGUIARROOM_AGUIAR_SMOKING, MOVEMENT_TYPE_SMOKING_LOOP)
    release
    end
}

script AguiarRoom_EventScript_AguiarBattle {
    if (!flag(FLAG_WAH_CHALLENGE_COMPLETED)) {
        trainerbattle_no_intro(TRAINER_WAH_ADMIN_AGUIAR_MAIN, AguiarRoom_Text_AguiarDefeated)
    } else {
        setvar(VAR_0x8004, TEAM_INDEX_AGUIAR)
        special(ShouldUseAlternativeTeam)
        if (var(VAR_RESULT) == 0) {
            trainerbattle_no_intro(TRAINER_WAH_ADMIN_AGUIAR_MAIN, AguiarRoom_Text_AguiarDefeated)
        } else {
            trainerbattle_no_intro(TRAINER_WAH_ADMIN_AGUIAR_ALTERNATIVE, AguiarRoom_Text_AguiarDefeated)
        }
    }
    special(HealPlayerParty)
    return
}

text AguiarRoom_Text_AguiarDefeated {
    format("¡Vaya combate! Me lo he pasado de\nlujo.")
}
