
mapscripts OmegaRoom_MapScripts {
     MAP_SCRIPT_ON_WARP_INTO_MAP_TABLE [
        VAR_TEMP_8, 0: OmegaRoom_OnWarp_PlayerTurnNorth
    ]
    MAP_SCRIPT_ON_LOAD: OmegaRoom_OnLoad
}


script OmegaRoom_OnLoad {
    if(flag(FLAG_DEFEATED_ADMIN_OMEGA)){
        call(OmegaRoom_EventScript_BrokenCapsule)
        call(WahRoomsShared_SetDoorAsOpen)
    }
    end
}

script OmegaRoom_OnWarp_PlayerTurnNorth {
    turnobject(LOCALID_PLAYER, DIR_NORTH)
    end
}
script OmegaRoom_EventScript_StartChallenge {
    lock
    directmovementtopos(LOCALID_PLAYER, 6, 9)
    waitmovement(0)
    turnobject(LOCALID_PLAYER, DIR_NORTH)
    special(SpawnCameraObject)
    directcameramovementtopos(6, 6)
    waitmovement(0)
    delay(50)
    call(OmegaRoom_EventScript_OmegaAwakens)
    delay(10)
    setvar(VAR_0x8004, 1)  // vertical pan
    setvar(VAR_0x8005, 2)  // horizontal pan
    setvar(VAR_0x8006, 8)  // num shakes
    setvar(VAR_0x8007, 5)  // shake delay
    special(ShakeCamera)
    fadescreen(FADE_TO_BLACK)
    delay(30)
    call(OmegaRoom_EventScript_BrokenCapsule)
    showobjectat(LOCALID_OMEGAROOM_OMEGA, MAP_OMEGA_ROOM)
    setobjectmovementtype(LOCALID_OMEGAROOM_OMEGA, MOVEMENT_TYPE_FACE_DOWN)
    setobjectmovementtype2(LOCALID_OMEGAROOM_OMEGA, MOVEMENT_TYPE_FACE_DOWN)
    turnobject(LOCALID_OMEGAROOM_OMEGA, DIR_SOUTH)
    fadescreen(FADE_FROM_BLACK)
    playse(SE_PIN)
    applymovement(LOCALID_OMEGAROOM_OMEGA, moves(emote_double_exclamation_mark, jump_in_place_down, delay_16 * 2))
    msgbox(format("¡Ostras, un{A} nuev{OA} contrincante!"), MSGBOX_AUTOCLOSE, speaker = SP_NAME_OMEGA)  
    waitmovement(0)
    directcameramovementtopos(6, 9)
    waitmovement(0)
    special(RemoveCameraObject)
    applymovement(LOCALID_OMEGAROOM_OMEGA, moves(
        jump_2_down))
    applymovement(LOCALID_PLAYER, moves(delay_8, lock_facing_direction, jump_down, unlock_facing_direction, face_up))
    waitmovement(0)
    delay(20)
    playse(SE_PIN)
    applymovementandwait(LOCALID_PLAYER, Common_Movement_QuestionMarkWithDelay48)
    delay(30)
    msgbox(format("¡Bienvenid{OA} al primer encuentro del desafío de esta celebración!\pTe estabamos esperando."), MSGBOX_AUTOCLOSE, speaker = SP_NAME_OMEGA)
    playse(SE_PIN)
    applymovementandwait(LOCALID_OMEGAROOM_OMEGA, Common_Movement_HappyWithDelay48)
    delay(20)
    msgbox(format("¿List{OA} para tu primer combate? ¡Veamos si eres capaz de derrotarme!"), MSGBOX_AUTOCLOSE, speaker = SP_NAME_OMEGA)
    applymovementandwait(LOCALID_OMEGAROOM_OMEGA, Common_Movement_WalkInPlaceDown)
    delay(20)
    applymovementandwait(LOCALID_PLAYER, Common_Movement_WalkInPlaceUp)
    delay(10)
    playse(SE_PIN)
    applymovement(LOCALID_OMEGAROOM_OMEGA, moves(emote_versus, jump_in_place_down * 2, delay_16))
    msgbox(format("Perfecto, ¡vamos!"), MSGBOX_AUTOCLOSE, speaker = SP_NAME_OMEGA)
    waitmovement(0)
    trainerbattle_no_intro(TRAINER_WAH_ADMIN_OMEGA_MAIN, format("¡Qué nivel has demostrado! No esperaba un combate tan entretenido."))
    playse(SE_PIN)
    applymovementandwait(LOCALID_OMEGAROOM_OMEGA, moves(emote_x, lock_facing_direction, jump_up, unlock_facing_direction, face_down, delay_16 * 2))
    delay(40)
    playse(SE_PIN)
    applymovement(LOCALID_OMEGAROOM_OMEGA, Common_Movement_SurpriseWithDelay48)
    msgbox(format("¡Sí que sacaste los colmillos!"), MSGBOX_AUTOCLOSE, speaker = SP_NAME_OMEGA)
    waitmovement(0)
    delay(20)
    applymovementandwait(LOCALID_OMEGAROOM_OMEGA, Common_Movement_WalkDown)
    msgbox(format("Ha sido un buen combate. Me quedo con ganas de más, la verdad.\pTocará esperar a que completes el desafío para tener una revancha."), MSGBOX_AUTOCLOSE, speaker = SP_NAME_OMEGA)
    playse(SE_PIN)
    applymovementandwait(LOCALID_OMEGAROOM_OMEGA, Common_Movement_WalkInPlaceDown)
    msgbox(format("De todas formas, esta fue solo la primera sala; todavía tenés un largo camino por delante."), MSGBOX_AUTOCLOSE, speaker = SP_NAME_OMEGA)
    applymovement(LOCALID_OMEGAROOM_OMEGA, Common_Movement_JumpInPlaceDown2)
    delay(30)
    msgbox(format("Va a ser interesante ver qué tanto puedes avanzar en este desafío."), MSGBOX_AUTOCLOSE, speaker = SP_NAME_OMEGA)
    waitmovement(0)
    delay(30)
    msgbox(format("Yo sigo activo por aquí, pero muchos de tus próximos rivales ya se habían retirado… y han vuelto especialmente para esta celebración.\pPero recuerda, no subestimes a la vieja guardia. Son tercos, y cuando se ponen en serio, se nota."), MSGBOX_AUTOCLOSE, speaker = SP_NAME_OMEGA)
    playse(SE_PIN)
    applymovementandwait(LOCALID_OMEGAROOM_OMEGA, Common_Movement_SweatDropWithDelay48)
    delay(20)
    applymovementandwait(LOCALID_OMEGAROOM_OMEGA, Common_Movement_WalkInPlaceDown)
    call(WahRoomsShared_OpenDoor)
    msgbox(format("Te he abierto la puerta. Ahora avanza, y demuestra que estas aquí para lograr grandes cosas."), MSGBOX_AUTOCLOSE, speaker = SP_NAME_OMEGA)
    applymovementandwait(LOCALID_OMEGAROOM_OMEGA, moves(walk_up * 2, face_down))
    setflag(FLAG_DEFEATED_ADMIN_OMEGA)
    setvar(VAR_TEMP_0, 1)
    release
    end
}

script OmegaRoom_EventScript_OmegaAwakens {
    setmetatile(5, 4, METATILE_room_omega_OMEGA_AWAKENS_1, FALSE)
    setmetatile(6, 4, METATILE_room_omega_OMEGA_AWAKENS_2, FALSE)
    setmetatile(7, 4, METATILE_room_omega_OMEGA_AWAKENS_3, FALSE)
    setmetatile(5, 5, METATILE_room_omega_OMEGA_AWAKENS_4, FALSE)
    setmetatile(6, 5, METATILE_room_omega_OMEGA_AWAKENS_5, FALSE)
    setmetatile(7, 5, METATILE_room_omega_OMEGA_AWAKENS_6, FALSE)
    setmetatile(6, 6, METATILE_room_omega_OMEGA_AWAKENS_7, TRUE)
    special(DrawWholeMapView)
    return
}

script OmegaRoom_EventScript_BrokenCapsule {
    setmetatile(5, 4, METATILE_room_omega_CAPSULE_BROKEN_1, FALSE)
    setmetatile(6, 4, METATILE_room_omega_CAPSULE_BROKEN_2, FALSE)
    setmetatile(7, 4, METATILE_room_omega_CAPSULE_BROKEN_3, FALSE)
    setmetatile(5, 5, METATILE_room_omega_CAPSULE_BROKEN_4, FALSE)
    setmetatile(6, 5, METATILE_room_omega_CAPSULE_BROKEN_5, FALSE)
    setmetatile(7, 5, METATILE_room_omega_CAPSULE_BROKEN_6, FALSE)
    setmetatile(6, 6, METATILE_room_omega_CAPSULE_BROKEN_7, TRUE)
    special(DrawWholeMapView)
    return
}