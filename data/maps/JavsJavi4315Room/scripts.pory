mapscripts JavsJavi4315Room_MapScripts {
    MAP_SCRIPT_ON_TRANSITION: JavsJavi4315Room_OnTransition
    MAP_SCRIPT_ON_WARP_INTO_MAP_TABLE [
        VAR_TEMP_8, 0: JavsJavi4315Room_OnWarp_PlayerTurnNorth
    ]
    MAP_SCRIPT_ON_LOAD: JavsJavi4315Room_OnLoad
}

script JavsJavi4315Room_OnLoad {
    if(flag(FLAG_DEFEATED_ADMINS_JAVI4315_AND_JAVS)) {
        call(WahRoomsShared_SetDoorAsOpen)
    }
    end
}

script JavsJavi4315Room_OnTransition {
    if(!flag(FLAG_DEFEATED_ADMINS_JAVI4315_AND_JAVS)) {
        setobjectxy(LOCALID_JAVSJAVIROOM_JAVI4315, 3, 3)
        setobjectxy(LOCALID_JAVSJAVIROOM_JAVS, 4, 3)
        setobjectxyperm(LOCALID_JAVSJAVIROOM_JAVI4315, 3, 3)
        setobjectxyperm(LOCALID_JAVSJAVIROOM_JAVS, 4, 3)
        setobjectmovementtype(LOCALID_JAVSJAVIROOM_JAVI4315, MOVEMENT_TYPE_FACE_UP)
        setobjectmovementtype(LOCALID_JAVSJAVIROOM_JAVS, MOVEMENT_TYPE_FACE_UP)
    }
    end
}

script JavsJavi4315Room_OnWarp_PlayerTurnNorth {
    turnobject(LOCALID_PLAYER, DIR_NORTH)
    end
}

script JavsJavi4315Room_EventScript_MainTrigger {
    lockall
    if(flag(FLAG_DEFEATED_ADMINS_JAVI4315_AND_JAVS)) {
        releaseall
        end
    }
    directmovementtopos(LOCALID_PLAYER, 6, 7)
    waitmovement(0)
    turnobject(LOCALID_PLAYER, DIR_NORTH)
    getplayerxy(VAR_TEMP_0, VAR_TEMP_1)
    special(SpawnCameraObject)
    directcameramovementtopos(4, 4)
    waitmovement(0)
    msgbox(format("Tío… ¡pero qué pedazo de tiles nos han colocado aquí!"), speaker = SP_NAME_JAVI4315)
    applymovementandwait(LOCALID_JAVSJAVIROOM_JAVI4315, moves(lock_facing_direction, jump_in_place_up * 2, unlock_facing_direction))
    turnobject(LOCALID_JAVSJAVIROOM_JAVI4315, DIR_EAST)
    msgbox(format("A que molan mucho, ¿verdad, Javier?"), MSGBOX_AUTOCLOSE, speaker = SP_NAME_JAVI4315)
    playse(SE_PIN)
    applymovementandwait(LOCALID_JAVSJAVIROOM_JAVS, Common_Movement_AngryWithDelay48)
    delay(30)
    turnobject(LOCALID_JAVSJAVIROOM_JAVS, DIR_WEST)
    msgbox(format("Que no me llamo Javier…"), MSGBOX_AUTOCLOSE, speaker = SP_NAME_JAVS)
    applymovementandwait(LOCALID_JAVSJAVIROOM_JAVS, Common_Movement_WalkInPlaceLeft)
    delay(30)
    turnobject(LOCALID_JAVSJAVIROOM_JAVI4315, DIR_NORTH)
    turnobject(LOCALID_JAVSJAVIROOM_JAVS, DIR_NORTH)
    msgbox(format("Y eso no es todo, ¡mira esas paletas! Tan bien contrastadas y matizadas… ¿serán privadas?"), MSGBOX_AUTOCLOSE, speaker = SP_NAME_JAVI4315)
    playse(SE_PIN)
    applymovementandwait(LOCALID_JAVSJAVIROOM_JAVS, Common_Movement_AnnoyedWithDelay48)
    delay(50)
    applymovementandwait(LOCALID_PLAYER, Common_Movement_WalkInPlaceUp)
    turnobject(LOCALID_JAVSJAVIROOM_JAVI4315, DIR_SOUTH)
    turnobject(LOCALID_JAVSJAVIROOM_JAVS, DIR_SOUTH)
    applymovement(LOCALID_JAVSJAVIROOM_JAVI4315, Common_Movement_DoubleExclamationMarkWithDelay48)
    applymovement(LOCALID_JAVSJAVIROOM_JAVS, Common_Movement_DoubleExclamationMarkWithDelay48)
    waitmovement(0)
    applymovement(LOCALID_JAVSJAVIROOM_JAVI4315, moves(walk_right, walk_down, walk_right, walk_down, delay_16))
    applymovement(LOCALID_JAVSJAVIROOM_JAVS, moves(walk_right * 3, walk_down * 2))
    waitmovement(0)
    setobjectxyperm(LOCALID_JAVSJAVIROOM_JAVI4315, 5, 5)
    setobjectxyperm(LOCALID_JAVSJAVIROOM_JAVS, 7, 5)
    directcameramovementtoposwithvars(VAR_TEMP_0, VAR_TEMP_1)
    waitmovement(0)
    special(RemoveCameraObject)
    applymovementandwait(LOCALID_JAVSJAVIROOM_JAVI4315,moves(jump_in_place_down * 2))
    msgbox(format("Hombre… ¡hasta que alguien ha caído por aquí!"), MSGBOX_AUTOCLOSE, speaker = SP_NAME_JAVI4315)
    applymovementandwait(LOCALID_JAVSJAVIROOM_JAVS, Common_Movement_WalkInPlaceDown)
    delay(20)
    msgbox(format("¿Sabías que, de todas las desgracias que te pudieron haber pasado hoy, caer en nuestro piso era la peor?"), MSGBOX_AUTOCLOSE, speaker = SP_NAME_JAVS)
    delay(20)
    playbgm(MUS_ENCOUNTER_MAGMA, FALSE)
    applymovement(LOCALID_JAVSJAVIROOM_JAVI4315, Common_Movement_WalkInPlaceDown)
    msgbox(format("No has presenciado un enfrentamiento en equipos como el nuestro ni en tus mejores sueños…\p¡Y no volverás a verlo, así que disfrútalo!"), MSGBOX_AUTOCLOSE, speaker = SP_NAME_JAVI4315)
    waitmovement(0)
    delay(20)
    applymovement(LOCALID_JAVSJAVIROOM_JAVS, Common_Movement_JumpInPlaceDown)
    msgbox(format("Aquí la estrategia manda, ¡que no se te olvide!"), MSGBOX_AUTOCLOSE, speaker = SP_NAME_JAVS)
    waitmovement(0)
    delay(20)
    applymovement(LOCALID_JAVSJAVIROOM_JAVI4315, Common_Movement_WalkInPlaceDown)
    msgbox(format("¿Listo, Javs?"), MSGBOX_AUTOCLOSE, speaker = SP_NAME_JAVI4315)
    waitmovement(0)
    delay(10)
    applymovement(LOCALID_JAVSJAVIROOM_JAVS, Common_Movement_WalkInPlaceDown)
    msgbox(format("¡Listo, Javi69!"), MSGBOX_AUTOCLOSE, speaker = SP_NAME_JAVS)
    waitmovement(0)
    turnobject(LOCALID_JAVSJAVIROOM_JAVI4315, DIR_EAST)
    applymovementandwait(LOCALID_JAVSJAVIROOM_JAVI4315, Common_Movement_QuestionMarkWithDelay48)
    delay(20)
    turnobject(LOCALID_JAVSJAVIROOM_JAVI4315, DIR_SOUTH)
    delay(30)
    applymovement(LOCALID_JAVSJAVIROOM_JAVI4315, Common_Movement_WalkDown)
    applymovement(LOCALID_JAVSJAVIROOM_JAVS, Common_Movement_WalkDown)
    waitmovement(0)
    msgbox(format("¡Ahora!"), MSGBOX_AUTOCLOSE, speaker = SP_NAME_JAVS)
    playse(SE_PIN)
    applymovement(LOCALID_JAVSJAVIROOM_JAVI4315, moves(emote_versus, jump_in_place_down * 2, delay_16))
    applymovement(LOCALID_JAVSJAVIROOM_JAVS, moves(emote_versus, jump_in_place_down * 2, delay_16))
    waitmovement(0)
    call(JavsJavi4315Room_EventScript_JavsJavi4315Battle)
    call(JavsJavi4315Room_EventScript_PostBattle)
    releaseall
    end
}

script JavsJavi4315Room_EventScript_JavsJavi4315Battle {
      if (!flag(FLAG_WAH_CHALLENGE_COMPLETED)) {
        trainerbattle_two_trainers(TRAINER_WAH_ADMIN_JAVI4315_MAIN, JavsJavi4315Room_Text_Javi4315Defeated , TRAINER_WAH_ADMIN_JAVS_MAIN, JavsJavi4315Room_Text_JavsDefeated)
    } else {
        setvar(VAR_0x8004, TEAM_INDEX_JAVI4315)
        special(ShouldUseAlternativeTeam)
        copyvar(VAR_0x8005, VAR_RESULT)
        setvar(VAR_0x8004, TEAM_INDEX_JAVS)
        special(ShouldUseAlternativeTeam)
        // VAR_0x8005 = Javi4315 team, VAR_RESULT = Javs team
        if (var(VAR_0x8005) == 0) {
            if (var(VAR_RESULT) == 0) {
                trainerbattle_two_trainers(TRAINER_WAH_ADMIN_JAVI4315_MAIN, JavsJavi4315Room_Text_Javi4315Defeated , TRAINER_WAH_ADMIN_JAVS_MAIN, JavsJavi4315Room_Text_JavsDefeated)
            } else {
                trainerbattle_two_trainers(TRAINER_WAH_ADMIN_JAVI4315_MAIN, JavsJavi4315Room_Text_Javi4315Defeated , TRAINER_WAH_ADMIN_JAVS_ALTERNATIVE, JavsJavi4315Room_Text_JavsDefeated)
            }
        } else {
            if (var(VAR_RESULT) == 0) {
                trainerbattle_two_trainers(TRAINER_WAH_ADMIN_JAVI4315_ALTERNATIVE, JavsJavi4315Room_Text_Javi4315Defeated , TRAINER_WAH_ADMIN_JAVS_MAIN, JavsJavi4315Room_Text_JavsDefeated)
            } else {
                trainerbattle_two_trainers(TRAINER_WAH_ADMIN_JAVI4315_ALTERNATIVE, JavsJavi4315Room_Text_Javi4315Defeated , TRAINER_WAH_ADMIN_JAVS_ALTERNATIVE, JavsJavi4315Room_Text_JavsDefeated)
            }
        }
    }
    special(HealPlayerParty)
    return
}

text JavsJavi4315Room_Text_Javi4315Defeated {
    format("¿Acaso hemos perdido, Javs?")
}

text JavsJavi4315Room_Text_JavsDefeated {
    format("¿Cómo puede ser? ¡Mi estrategia era infalible!")
}

script JavsJavi4315Room_EventScript_PostBattle {
    playse(SE_PIN)
    applymovement(LOCALID_JAVSJAVIROOM_JAVS, moves(emote_surprise, lock_facing_direction, walk_up, unlock_facing_direction, face_down))
    applymovementandwait(LOCALID_JAVSJAVIROOM_JAVI4315, moves(emote_surprise, lock_facing_direction, walk_up, unlock_facing_direction, face_down))
    waitmovement(0)
    delay(40)
    applymovement(LOCALID_JAVSJAVIROOM_JAVS, Common_Movement_WalkInPlaceDown)
    msgbox(format("Me pasé semanas planeando esta estrategia… Admito que eso ha dolido más de lo que esperaba."), MSGBOX_AUTOCLOSE, speaker = SP_NAME_JAVS)
    waitmovement(0)
    applymovementandwait(LOCALID_JAVSJAVIROOM_JAVS, Common_Movement_SweatDropWithDelay48)
    turnobject(LOCALID_JAVSJAVIROOM_JAVI4315, DIR_EAST)
    applymovementandwait(LOCALID_JAVSJAVIROOM_JAVI4315, Common_Movement_WalkInPlaceRight)
    msgbox(format("¡Y eso que armé mi equipo exactamente como me pediste!"), MSGBOX_AUTOCLOSE, speaker = SP_NAME_JAVI4315)
    applymovementandwait(LOCALID_JAVSJAVIROOM_JAVS, Common_Movement_WalkUp)
    msgbox(format("La apertura era perfecta: sol activo y presión desde el turno uno. ¿Qué habré hecho mal?"), MSGBOX_AUTOCLOSE, speaker = SP_NAME_JAVS)
    playse(SE_PIN)
    applymovementandwait(LOCALID_JAVSJAVIROOM_JAVS, Common_Movement_ThinkingWithDelay48)
    delay(20)
    applymovementandwait(LOCALID_JAVSJAVIROOM_JAVS, moves(walk_down, delay_16, face_left))
    msgbox(format("La estrategia estuvo bien, pero simplemente tuvimos un{A} oponente demasiado fuerte."), MSGBOX_AUTOCLOSE, speaker = SP_NAME_JAVS)
    delay(20)
    playse(SE_PIN)
    applymovement(LOCALID_JAVSJAVIROOM_JAVI4315, moves(emote_happy, walk_in_place_right, delay_16 * 2))
    msgbox(format("¡Al menos nos divertimos!"), MSGBOX_AUTOCLOSE, speaker = SP_NAME_JAVI4315)
    waitmovement(0)
    delay(20)
    turnobject(LOCALID_JAVSJAVIROOM_JAVI4315, DIR_SOUTH)
    turnobject(LOCALID_JAVSJAVIROOM_JAVS, DIR_SOUTH)
    delay(10)
    applymovement(LOCALID_JAVSJAVIROOM_JAVS, Common_Movement_WalkInPlaceDown)
    msgbox(format("Te abriremos la puerta, que ya te falta menos de la mitad."), MSGBOX_AUTOCLOSE, speaker = SP_NAME_JAVS)
    waitmovement(0)
    call(WahRoomsShared_OpenDoor)
    delay(30)
    applymovement(LOCALID_JAVSJAVIROOM_JAVI4315, Common_Movement_WalkInPlaceDown)
    msgbox(format("Nosotros nos quedaremos por aquí contemplando la sala."), MSGBOX_AUTOCLOSE, speaker = SP_NAME_JAVI4315)
    waitmovement(0)
    setflag(FLAG_DEFEATED_ADMINS_JAVI4315_AND_JAVS)
    setvar(VAR_JAVS_JAVI4315_ROOM_STATE, 1)
    return
}

script JavsJavi4315Room_EventScript_Javi4315 {
    msgbox(format("Siempre quise crear algo que se sintiera {COLOR GREEN}inmortal{COLOR DARK_GRAY}.\pSupongo que por eso sigo aquí, dándole vueltas a todo."), MSGBOX_NPC, speaker = SP_NAME_JAVI4315)
    end
}

script JavsJavi4315Room_EventScript_Javs {
    msgbox(format("¿Te gusta como se ve la sala? ¡Ni se te ocurra copiar sus colores sin mi permiso!"), MSGBOX_NPC, speaker = SP_NAME_JAVS)
    end
}
