mapscripts JavsJavi4315Room_MapScripts {
    MAP_SCRIPT_ON_TRANSITION: JavsJavi4315Room_OnTransition
    MAP_SCRIPT_ON_WARP_INTO_MAP_TABLE [
        VAR_TEMP_8, 0: JavsJavi4315Room_OnWarp_PlayerTurnNorth
    ]
    MAP_SCRIPT_ON_LOAD: JavsJavi4315Room_OnLoad
}

script JavsJavi4315Room_OnLoad {
    if(flag(FLAG_DEFEATED_ADMINS_JAVI4315_AND_JAVS)) {
        call(WahRoomsShared_SetDoorAsOpen)
    }
    end
}

script JavsJavi4315Room_OnTransition {
    if(flag(FLAG_DEFEATED_ADMINS_JAVI4315_AND_JAVS)) {
        setvar(VAR_JAVS_JAVI4315_ROOM_STATE, 2)
        setobjectxy(LOCALID_JAVSJAVIROOM_JAVI4315, 5, 9)
        setobjectxy(LOCALID_JAVSJAVIROOM_JAVS, 7, 9)
        setobjectxyperm(LOCALID_JAVSJAVIROOM_JAVI4315, 5, 9)
        setobjectxyperm(LOCALID_JAVSJAVIROOM_JAVS, 7, 9)
        setobjectmovementtype(LOCALID_JAVSJAVIROOM_JAVI4315, MOVEMENT_TYPE_FACE_DOWN)
        setobjectmovementtype(LOCALID_JAVSJAVIROOM_JAVS, MOVEMENT_TYPE_FACE_DOWN)
        setobjectmovementtype2(LOCALID_JAVSJAVIROOM_JAVI4315, MOVEMENT_TYPE_FACE_DOWN)
        setobjectmovementtype2(LOCALID_JAVSJAVIROOM_JAVS, MOVEMENT_TYPE_FACE_DOWN)
    } else {
        setvar(VAR_JAVS_JAVI4315_ROOM_STATE, 0)
        setobjectxy(LOCALID_JAVSJAVIROOM_JAVI4315, 5, 5)
        setobjectxy(LOCALID_JAVSJAVIROOM_JAVS, 7, 5)
        setobjectxyperm(LOCALID_JAVSJAVIROOM_JAVI4315, 5, 5)
        setobjectxyperm(LOCALID_JAVSJAVIROOM_JAVS, 7, 5)
        setobjectmovementtype(LOCALID_JAVSJAVIROOM_JAVI4315, MOVEMENT_TYPE_FACE_UP)
        setobjectmovementtype(LOCALID_JAVSJAVIROOM_JAVS, MOVEMENT_TYPE_FACE_UP)
        setobjectmovementtype2(LOCALID_JAVSJAVIROOM_JAVI4315, MOVEMENT_TYPE_FACE_UP)
        setobjectmovementtype2(LOCALID_JAVSJAVIROOM_JAVS, MOVEMENT_TYPE_FACE_UP)
    }
    end
}

script JavsJavi4315Room_OnWarp_PlayerTurnNorth {
    turnobject(LOCALID_PLAYER, DIR_NORTH)
    end
}

script JavsJavi4315Room_EventScript_MainTrigger {
    lockall
    if(flag(FLAG_DEFEATED_ADMINS_JAVI4315_AND_JAVS)) {
        releaseall
        end
    }
    setvar(VAR_JAVS_JAVI4315_ROOM_STATE, 1)
    getplayerxy(VAR_TEMP_0, VAR_TEMP_1)
    special(SpawnCameraObject)
    directcameramovementtopos(6, 4)
    waitmovement(0)
    msgbox(format("Tío… ¡pero qué pedazo de tiles nos han colocado aquí!"), MSGBOX_AUTOCLOSE, speaker = SP_NAME_JAVI4315)
    applymovementandwait(LOCALID_JAVSJAVIROOM_JAVI4315, moves(lock_facing_direction, jump_in_place_up * 2, unlock_facing_direction))
    turnobject(LOCALID_JAVSJAVIROOM_JAVI4315, DIR_EAST)
    msgbox(format("A que molan mucho, ¿verdad, Javier?"), MSGBOX_AUTOCLOSE, speaker = SP_NAME_JAVI4315)
    applymovementandwait(LOCALID_JAVSJAVIROOM_JAVS, moves(emote_angry))
    delay(30)
    turnobject(LOCALID_JAVSJAVIROOM_JAVS, DIR_WEST)
    msgbox(format("Que no me llamo Javier…"), MSGBOX_AUTOCLOSE, speaker = SP_NAME_JAVS)
    applymovementandwait(LOCALID_JAVSJAVIROOM_JAVS, Common_Movement_WalkInPlaceLeft)
    delay(30)
    turnobject(LOCALID_JAVSJAVIROOM_JAVI4315, DIR_NORTH)
    msgbox(format("Y eso no es todo, ¡mira esas paletas! Tan bien contrastadas y matizadas… ¿serán privadas?"), MSGBOX_AUTOCLOSE, speaker = SP_NAME_JAVI4315)
    turnobject(LOCALID_JAVSJAVIROOM_JAVS, DIR_NORTH)
    applymovementandwait(LOCALID_JAVSJAVIROOM_JAVS, moves(emote_thinking))
    turnobject(LOCALID_JAVSJAVIROOM_JAVI4315, DIR_SOUTH)
    turnobject(LOCALID_JAVSJAVIROOM_JAVS, DIR_SOUTH)
    applymovement(LOCALID_JAVSJAVIROOM_JAVI4315, Common_Movement_DoubleExclamationMarkWithDelay48)
    applymovement(LOCALID_JAVSJAVIROOM_JAVS, Common_Movement_DoubleExclamationMarkWithDelay48)
    waitmovement(0)
    applymovement(LOCALID_JAVSJAVIROOM_JAVI4315, moves(walk_down * 4, face_down))
    applymovement(LOCALID_JAVSJAVIROOM_JAVS, moves(walk_down * 4, face_down))
    waitmovement(0)
    setobjectxyperm(LOCALID_JAVSJAVIROOM_JAVI4315, 5, 9)
    setobjectxyperm(LOCALID_JAVSJAVIROOM_JAVS, 7, 9)
    directcameramovementtoposwithvars(VAR_TEMP_0, VAR_TEMP_1)
    waitmovement(0)
    directmovementtopos(LOCALID_PLAYER, 6, 10)
    waitmovement(LOCALID_PLAYER)
    applymovementandwait(LOCALID_JAVSJAVIROOM_JAVI4315, moves(lock_facing_direction, jump_in_place_down * 2, unlock_facing_direction))
    msgbox(format("Hombre… ¡hasta que alguien ha caído por aquí!"), MSGBOX_AUTOCLOSE, speaker = SP_NAME_JAVI4315)
    applymovementandwait(LOCALID_JAVSJAVIROOM_JAVS, Common_Movement_WalkInPlaceDown)
    msgbox(format("¿Sabías que, de todas las desgracias que te pudieron haber pasado hoy, caer en nuestro piso era la peor?"), MSGBOX_AUTOCLOSE, speaker = SP_NAME_JAVS)
    playbgm(MUS_ENCOUNTER_MAGMA, FALSE)
    msgbox(format("No has presenciado un enfrentamiento en equipos como el nuestro ni en tus mejores sueños… ¡y no volverás a verlo, así que disfrútalo!"), MSGBOX_AUTOCLOSE, speaker = SP_NAME_JAVI4315)
    applymovementandwait(LOCALID_JAVSJAVIROOM_JAVI4315, Common_Movement_WalkInPlaceDown)
    msgbox(format("Aquí la estrategia manda, ¡que no se te olvide!"), MSGBOX_AUTOCLOSE, speaker = SP_NAME_JAVS)
    applymovementandwait(LOCALID_JAVSJAVIROOM_JAVS, moves(lock_facing_direction, jump_in_place_down, unlock_facing_direction))
    msgbox(format("¿Listo, Javs?"), MSGBOX_AUTOCLOSE, speaker = SP_NAME_JAVI4315)
    applymovementandwait(LOCALID_JAVSJAVIROOM_JAVS, Common_Movement_WalkInPlaceDown)
    msgbox(format("¡Listo, Javi69!"), MSGBOX_AUTOCLOSE, speaker = SP_NAME_JAVS)
    applymovementandwait(LOCALID_JAVSJAVIROOM_JAVI4315, Common_Movement_QuestionMarkWithDelay48)
    delay(30)
    applymovement(LOCALID_JAVSJAVIROOM_JAVI4315, Common_Movement_WalkDown)
    applymovement(LOCALID_JAVSJAVIROOM_JAVS, Common_Movement_WalkDown)
    waitmovement(0)
    msgbox(format("¡Ahora!"), MSGBOX_AUTOCLOSE)
    applymovement(LOCALID_JAVSJAVIROOM_JAVI4315, moves(emote_versus))
    applymovement(LOCALID_JAVSJAVIROOM_JAVS, moves(emote_versus))
    waitmovement(0)
    trainerbattle_two_trainers(TRAINER_WAH_ADMIN_JAVI4315_MAIN, format("") , TRAINER_WAH_ADMIN_JAVS_MAIN, format(""))
    call(JavsJavi4315Room_EventScript_PostBattle)
    special(RemoveCameraObject)
    releaseall
    end
}

script JavsJavi4315Room_EventScript_PostBattle {
    applymovement(LOCALID_JAVSJAVIROOM_JAVS, moves(lock_facing_direction, walk_up, unlock_facing_direction, face_down))
    applymovementandwait(LOCALID_JAVSJAVIROOM_JAVI4315, moves(lock_facing_direction, walk_up, unlock_facing_direction, face_down))
    waitmovement(0)
    msgbox(format("Me pasé semanas planeando esta estrategia... Admito que eso ha dolido más de lo que esperaba."), MSGBOX_AUTOCLOSE, speaker = SP_NAME_JAVS)
    applymovementandwait(LOCALID_JAVSJAVIROOM_JAVS, Common_Movement_SweatDropWithDelay48)
    turnobject(LOCALID_JAVSJAVIROOM_JAVI4315, DIR_EAST)
    applymovementandwait(LOCALID_JAVSJAVIROOM_JAVI4315, Common_Movement_WalkInPlaceRight)
    msgbox(format("¡Y eso que armé mi equipo exactamente como me pediste!"), MSGBOX_AUTOCLOSE, speaker = SP_NAME_JAVI4315)
    applymovementandwait(LOCALID_JAVSJAVIROOM_JAVS, moves(walk_up * 2, face_up))
    msgbox(format("La apertura era perfecta: sol activo y presión desde el turno uno. ¿Qué habré hecho mal?"), MSGBOX_AUTOCLOSE, speaker = SP_NAME_JAVS)
    applymovementandwait(LOCALID_JAVSJAVIROOM_JAVS, moves(emote_thinking))
    applymovementandwait(LOCALID_JAVSJAVIROOM_JAVS, moves(walk_down * 2, face_left))
    msgbox(format("La estrategia estuvo bien, pero simplemente tuvimos un{A} oponente demasiado fuerte."), MSGBOX_AUTOCLOSE, speaker = SP_NAME_JAVS)
    msgbox(format("¡Al menos nos divertimos!"), MSGBOX_AUTOCLOSE, speaker = SP_NAME_JAVI4315)
    applymovementandwait(LOCALID_JAVSJAVIROOM_JAVI4315, moves(emote_happy))
    turnobject(LOCALID_JAVSJAVIROOM_JAVI4315, DIR_SOUTH)
    turnobject(LOCALID_JAVSJAVIROOM_JAVS, DIR_SOUTH)
    msgbox(format("Te abriremos la puerta, que ya estás en la recta final."), MSGBOX_AUTOCLOSE, speaker = SP_NAME_JAVS)
    call(WahRoomsShared_OpenDoor)
    delay(30)
    msgbox(format("Nosotros nos quedaremos por aquí contemplando la sala."), MSGBOX_AUTOCLOSE, speaker = SP_NAME_JAVI4315)
    setflag(FLAG_DEFEATED_ADMINS_JAVI4315_AND_JAVS)
    setvar(VAR_JAVS_JAVI4315_ROOM_STATE, 2)
    return
}

script JavsJavi4315Room_EventScript_Javi4315 {
    lock
    faceplayer
    if(!flag(FLAG_DEFEATED_ADMINS_JAVI4315_AND_JAVS)) {
        release
        call(JavsJavi4315Room_EventScript_MainTrigger)
        end
    }
    msgbox(format("Siempre quise crear algo que se sintiera \c2inmortal\c0. Supongo que por eso sigo aquí, dándole vueltas a todo."), speaker = SP_NAME_JAVI4315)
    release
    end
}

script JavsJavi4315Room_EventScript_Javs {
    lock
    faceplayer
    if(!flag(FLAG_DEFEATED_ADMINS_JAVI4315_AND_JAVS)) {
        release
        call(JavsJavi4315Room_EventScript_MainTrigger)
        end
    }
    applymovementandwait(LOCALID_JAVSJAVIROOM_JAVS, moves(face_left, face_up, face_right, face_down))
    msgbox(format("¿Te gusta como se ve la sala? ¡Ni se te ocurra copiar sus colores sin mi permiso!"), speaker = SP_NAME_JAVS)
    release
    end
}
