const VAR_TEMP_0_EING_ROOM_STATE = VAR_TEMP_0
const FLAG_TEMP_1_EING_ROOM_HIDE_EING_FISHING = FLAG_TEMP_1
const FLAG_TEMP_2_EING_ROOM_HIDE_TRAINER_IN_WATER = FLAG_TEMP_2

mapscripts EingRoom_MapScripts {
    MAP_SCRIPT_ON_FRAME_TABLE [
        VAR_TEMP_0_EING_ROOM_STATE, 0: EingRoom_OnFrame
    ]
    MAP_SCRIPT_ON_TRANSITION: EingRoom_OnTransition
    MAP_SCRIPT_ON_LOAD: EingRoom_OnLoad
    MAP_SCRIPT_ON_WARP_INTO_MAP_TABLE [
        VAR_TEMP_8, 0: EingRoom_OnWarp_PlayerTurnNorth
    ]
}

script EingRoom_OnLoad {
    if(flag(FLAG_DEFEATED_ADMIN_EING)){
        call(WahRoomsShared_SetDoorAsOpen)
    }
    end
}

script EingRoom_OnWarp_PlayerTurnNorth {
    turnobject(LOCALID_PLAYER, DIR_NORTH)
    end
}

script EingRoom_OnTransition {
    if(var(VAR_TEMP_0_EING_ROOM_STATE) == 0) {
        setflag(FLAG_TEMP_1_EING_ROOM_HIDE_EING_FISHING)
        setflag(FLAG_TEMP_2_EING_ROOM_HIDE_TRAINER_IN_WATER)
    }
    if(var(VAR_TEMP_0_EING_ROOM_STATE) == 1){
        setobjectmovementtype(LOCALID_EINGROOM_EING, MOVEMENT_TYPE_FACE_LEFT)
    }
    end
}

script EingRoom_OnFrame {
    release
    getplayerxy(VAR_TEMP_A, VAR_TEMP_B)
    special(SpawnCameraObject)
    directcameramovementtopos(6, 6)
    waitmovement(0)
    applymovementandwait(LOCALID_EINGROOM_EING, Common_Movement_WalkInPlaceDown)
    msgbox(format("Malditos Xiros y Serg!o, que se han pirado dejándome a mí con el marrón de administrar la página web y el foro…"), MSGBOX_AUTOCLOSE, speaker = SP_NAME_EING)
    playse(SE_LEDGE)
    applymovementandwait(LOCALID_EINGROOM_EING, moves(jump_in_place_down))
    waitse
    playse(SE_PIN)
    applymovement(LOCALID_EINGROOM_EING, moves(emote_angry))
    msgbox(format("¡Y yo sin las malditas credenciales!"), MSGBOX_AUTOCLOSE, speaker = SP_NAME_EING)
    waitmovement(0)
    delay(30)
    applymovementandwait(LOCALID_EINGROOM_EING, moves(walk_right * 2, walk_left * 2, walk_right * 2, walk_left * 2))
    playse(SE_LEDGE)
    applymovementandwait(LOCALID_EINGROOM_EING, moves(jump_in_place_left))
    waitse
    playse(SE_PIN)
    applymovementandwait(LOCALID_EINGROOM_EING, moves(emote_idea))
    msgbox(format("A ver si las han tirado al fondo del mar como una prueba para desafiarme y ver si soy digno de administrar WAH…\p¡Voy a ver si las pesco!"), MSGBOX_AUTOCLOSE, speaker = SP_NAME_EING)
    applymovementandwait(LOCALID_EINGROOM_EING, moves(walk_left, walk_in_place_left))
    hideobjectat(LOCALID_EINGROOM_EING, MAP_EING_ROOM)
    clearflag(FLAG_TEMP_1_EING_ROOM_HIDE_EING_FISHING)
    addobject(LOCALID_EINGROOM_EING_FISHING)
    showobjectat(LOCALID_EINGROOM_EING_FISHING, MAP_EING_ROOM)
    playse(SE_M_DOUBLE_TEAM)
    applymovement(LOCALID_EINGROOM_EING_FISHING, Common_Movement_Fishing)
    delay(90)
    playse(SE_M_DIVE)
    waitmovement(0)
    setflag(FLAG_TEMP_1_EING_ROOM_HIDE_EING_FISHING)
    removeobject(LOCALID_EINGROOM_EING_FISHING)
    showobjectat(LOCALID_EINGROOM_EING, MAP_EING_ROOM)
    clearflag(FLAG_TEMP_2_EING_ROOM_HIDE_TRAINER_IN_WATER)
    addobject(LOCALID_EINGROOM_TRAINER_IN_WATER)
    showobjectat(LOCALID_EINGROOM_TRAINER_IN_WATER, MAP_EING_ROOM)
    waitse
    playse(SE_LEDGE)
    applymovementandwait(LOCALID_EINGROOM_EING, moves(jump_in_place_left))
    waitse
    playse(SE_LEDGE)
    applymovementandwait(LOCALID_EINGROOM_EING, moves(jump_in_place_left))
    waitse
    playse(SE_LEDGE)
    applymovementandwait(LOCALID_EINGROOM_EING, moves(jump_in_place_left))
    waitse
    playse(SE_PIN)
    applymovementandwait(LOCALID_EINGROOM_EING, moves(emote_angry))
    msgbox(format("¡Maldita sea! ¿Otro entrenador?"), MSGBOX_AUTOCLOSE, speaker = SP_NAME_EING)
    delay(30)
    applymovementandwait(LOCALID_EINGROOM_EING, Common_Movement_FaceRight)
    msgbox(format("Esto se está pareciendo a mi rutina de wildbattles dobles del Hoenn Adventures…"), MSGBOX_AUTOCLOSE, speaker = SP_NAME_EING)
    turnobject(LOCALID_EINGROOM_EING, DIR_WEST)
    directcameramovementtoposwithvars(VAR_TEMP_A, VAR_TEMP_B)
    waitmovement(0)
    setvar(VAR_TEMP_0_EING_ROOM_STATE, 1)
    setobjectmovementtype2(LOCALID_EINGROOM_EING, MOVEMENT_TYPE_FACE_LEFT)
    special(RemoveCameraObject)
    release
    end
}

script EingRoom_EventScript_Eing {
    lock
    faceplayer
    if(flag(FLAG_DEFEATED_ADMIN_EING)) {
        playse(SE_PIN)
        applymovement(LOCALID_EINGROOM_EING, Common_Movement_AngryWithDelay48)
        msgbox(format("¿Pero mira que eres pesado eh? ¿Tú no tienes amigos u otra persona a la que molestar?"), MSGBOX_AUTOCLOSE, speaker = SP_NAME_EING)
        waitmovement(0)
        release
        end
    }
    playse(SE_PIN)
    applymovementandwait(LOCALID_EINGROOM_EING, moves(emote_double_exclamation_mark))
    waitse
    playse(SE_LEDGE)
    applymovementandwait(LOCALID_EINGROOM_EING, moves(lock_facing_direction, jump_in_place_down))
    waitse
    playse(SE_LEDGE)
    applymovementandwait(LOCALID_EINGROOM_EING, moves(jump_in_place_down, unlock_facing_direction, face_player))
    waitse
    msgbox(format("¡Coño si tenemos visita! ¿Qué quieres? ¿No ves que estoy ocupado?"), MSGBOX_AUTOCLOSE, speaker = SP_NAME_EING)
    playse(SE_PIN)
    applymovementandwait(LOCALID_EINGROOM_EING, Common_Movement_Angry)
    call(EingRoom_EventScript_WalkInPlaceEingFacingPlayer)
    delay(40)
    msgbox(format("Si quieres un cambio de nick, tendrás que esperar y como te pongas tonto te baneo, ¿eh?"), MSGBOX_AUTOCLOSE, speaker = SP_NAME_EING)
    fadescreen(FADE_TO_BLACK)
    delay(30)
    fadescreen(FADE_FROM_BLACK)
    playse(SE_LEDGE)
    applymovement(LOCALID_EINGROOM_EING, moves(
        lock_facing_direction, 
        jump_in_place_down, 
        unlock_facing_direction, 
        face_player))
    msgbox(format("¿Cómo? ¿Que quieres retarme a un combate Pokémon?"), MSGBOX_AUTOCLOSE, speaker = SP_NAME_EING)
    waitmovement(0)
    playse(SE_PIN)
    applymovementandwait(LOCALID_EINGROOM_EING, moves(
        face_away_player
        emote_thinking
        delay_16 * 3
    ))
    delay(30)
    call(EingRoom_EventScript_JumpToPlayerDirection)
    msgbox(format("Está bien, prepárate porque iré con todo, ¡luego no me llores!"), MSGBOX_AUTOCLOSE, speaker = SP_NAME_EING)
    playse(SE_PIN)
    applymovementandwait(LOCALID_EINGROOM_EING, Common_Movement_Versus)
    call(EingRoom_EventScript_WalkInPlaceEingFacingPlayer)
    delay(30)
    call(EingRoom_EventScript_EingBattle)
    msgbox(format("Enhorabuena chaval, ahora avanza a la siguiente sala y déjame tranquilo, que estaba muy ocupado..."), speaker = SP_NAME_EING)
    call(WahRoomsShared_OpenDoor)
    applymovementandwait(LOCALID_EINGROOM_EING, Common_Movement_WalkInPlaceLeft)
    setflag(FLAG_DEFEATED_ADMIN_EING)
    release
    end
}

script EingRoom_EventScript_EingBattle {
    if (!flag(FLAG_WAH_CHALLENGE_COMPLETED)) {
        trainerbattle_no_intro(TRAINER_WAH_ADMIN_EING_MAIN, EingRoom_Text_EingDefeated)
    } else {
        setvar(VAR_0x8004, TEAM_INDEX_EING)
        special(ShouldUseAlternativeTeam)
        if (var(VAR_RESULT) == 0) {
            trainerbattle_no_intro(TRAINER_WAH_ADMIN_EING_MAIN, EingRoom_Text_EingDefeated)
        } else {
            trainerbattle_no_intro(TRAINER_WAH_ADMIN_EING_ALTERNATIVE, EingRoom_Text_EingDefeated)
        }
    }
    special(HealPlayerParty)
    return
}

text EingRoom_Text_EingDefeated {
    format("¡Míralo, si parecía tonto al principio!")
}

script EingRoom_EventScript_WalkInPlaceEingFacingPlayer {
    specialvar(VAR_RESULT, GetPlayerFacingDirection)
    switch(var(VAR_RESULT)) {
        case DIR_NORTH:
            applymovementandwait(LOCALID_EINGROOM_EING, Common_Movement_WalkInPlaceDown)
            break
        case DIR_SOUTH:
            applymovementandwait(LOCALID_EINGROOM_EING, Common_Movement_WalkInPlaceUp)
            break
        case DIR_WEST:
            applymovementandwait(LOCALID_EINGROOM_EING, Common_Movement_WalkInPlaceRight)
            break
        case DIR_EAST:
            applymovementandwait(LOCALID_EINGROOM_EING, Common_Movement_WalkInPlaceLeft)
            break
    }
    return
}


script EingRoom_EventScript_JumpToPlayerDirection {
    specialvar(VAR_RESULT, GetPlayerFacingDirection)
    playse(SE_LEDGE)
    switch(var(VAR_RESULT)) {
        case DIR_NORTH:
            applymovementandwait(LOCALID_EINGROOM_EING, moves(jump_in_place_up_down))
            break
        case DIR_SOUTH:
            applymovementandwait(LOCALID_EINGROOM_EING, moves(jump_in_place_down_up))
            break
        case DIR_WEST:
            applymovementandwait(LOCALID_EINGROOM_EING, moves(jump_in_place_left_right))
            break
        case DIR_EAST:
            applymovementandwait(LOCALID_EINGROOM_EING, moves(jump_in_place_right_left))
            break
    }
    return
}

